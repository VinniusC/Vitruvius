/* Crater 2021 */

/* PH SEASONS */

	#define PH_FROZEN

#include_drs F_seasons.inc

<PLAYER_SETUP>
  random_placement
  /* grouped_by_team */
  ai_info_map_type OASIS 0 0 0


<LAND_GENERATION>
base_terrain LAYER_A
enable_waves 0 

#const CRATER_LAYER_C 76
#const CRATER_LAYER_D 102

create_player_lands 
{
  terrain_type				CRATER_LAYER_C 
  land_percent                     100
  base_size                        12
if TINY_MAP
  circle_radius 		   26 0
elseif SMALL_MAP
  circle_radius 		   26 0
else
  circle_radius 		   30 0
endif
  other_zone_avoidance_distance    0
  border_fuzziness	10
if TINY_MAP
  bottom_border 20
  top_border 20
  left_border 20
  right_border 20
elseif SMALL_MAP
  bottom_border 18
  top_border 18
  left_border 18
  right_border 18
else
  bottom_border 16
  top_border 16
  left_border 16
  right_border 16
endif
  zone 1
}

create_land
{
  terrain_type			   DIRT2
  land_percent                     100
  base_size                        1
  other_zone_avoidance_distance    0
if TINY_MAP
  bottom_border 46
  top_border 46
  left_border 46
  right_border 46
elseif SMALL_MAP
  bottom_border 44
  top_border 44
  left_border 44
  right_border 44
else
  bottom_border 42
  top_border 42
  left_border 42
  right_border 42
endif
}


/* ****************************************************** */
<TERRAIN_GENERATION>

  create_terrain WOODIES
{
  base_terrain LAYER_A
	land_percent 100
  number_of_clumps 100
  height_limits 6 7
  spacing_to_other_terrain_types 10
}

  create_terrain DLC_ROCK
{
  base_terrain LAYER_A
  land_percent 100
  number_of_clumps 100
  height_limits 2 5
}

  create_terrain DLC_ROCK
{
  base_terrain CRATER_LAYER_C 
  land_percent 100
  number_of_clumps 100
  height_limits 2 5
}


  create_terrain DLC_ROCK
{
  base_terrain LAYER_A
  land_percent 100
  number_of_clumps 100
  height_limits 2 5
}


  create_terrain DLC_ROCK
{
  base_terrain CRATER_LAYER_C 
  land_percent 100
  number_of_clumps 100
  height_limits 2 5
}

  create_terrain DLC_DRYGRASS
{
  base_terrain CRATER_LAYER_C 
  land_percent 6
  number_of_clumps 20
  terrain_mask 1
}

  create_terrain DLC_DRYGRASS
{
  base_terrain LAYER_A
  land_percent 100
  number_of_clumps 100
  height_limits 0 3
}

  create_terrain DLC_DRYGRASS
{
  base_terrain LAYER_A
  land_percent 100
  number_of_clumps 100
  height_limits 0 3
}

  create_terrain DLC_DRYGRASS
{
  base_terrain LAYER_A
  land_percent 100
  number_of_clumps 100
  height_limits 0 3
}

  create_terrain DLC_DRYGRASS
{
  base_terrain LAYER_A
  land_percent 100
  number_of_clumps 100
  height_limits 0 3
}

  create_terrain DLC_ROCK
{
  base_terrain DLC_DRYGRASS
  land_percent 100
  number_of_clumps 100
  height_limits 1 1
  terrain_mask 1
}

  create_terrain DLC_ROCK
{
  base_terrain SNOW
  land_percent 6
  number_of_clumps 27
  height_limits 6 6
  terrain_mask 1
}

  create_terrain CRATER_LAYER_D
{
  base_terrain CRATER_LAYER_C 
  land_percent 10
  number_of_clumps 100
  terrain_mask 1
  spacing_to_other_terrain_types 3
}

  create_terrain CRATER_LAYER_D
{
  base_terrain CRATER_LAYER_C 
  land_percent 1
  number_of_clumps 20
  terrain_mask 1
}

  create_terrain CRATER_LAYER_D
{
  base_terrain DIRT2
  land_percent 1
  number_of_clumps 20
  terrain_mask 1
}

  create_terrain CRATER_LAYER_C
{
  base_terrain DIRT2
  land_percent 100
  number_of_clumps 1000
  terrain_mask 1
}

/* ****************************************************** */
<OBJECTS_GENERATION>

#define GNR_NORMALTC
#define GNR_STARTVILLS
#define GNR_CLASSICSCOUT

#define GNR_RESCLOSE

#define GNR_REGICIDECLASSIC

#define GNR_STARTINGGOLD

#define GNR_STARTGOLD744CL
#define GNR_SPLITSTARTGOLDS

#define GNR_STARTINGSTONE
#define GNR_STARTSTONE54CL
#define GNR_SPLITSTARTSTONES

#define GNR_GIVEHERD_A
#define GNR_GIVEHERD_B

#define GNR_HERD4CLASSIC_A
#define GNR_HERD22CLASSIC_B

#define GNR_GIVELURES
#define GNR_CLASSICLURES_ONE /* one boar only with 2x4 ibexes */

#define GNR_SPECIFICKER

/* 6ker on grass */

if INFINITE_RESOURCES
elseif DEATH_MATCH
else

create_object KERICEK
{
  number_of_objects          6
  group_placement_radius     3
  set_tight_grouping
  set_gaia_object_only
  set_place_for_every_player
  max_distance_to_players    60
  find_closest
		if EMPIRE_WARS
  actor_area 14
  actor_area_radius 1
		else
  actor_area 14
  actor_area_radius 4
  min_distance_group_placement 2
		endif
  terrain_to_place_on DLC_DRYGRASS
}

endif

#include_drs GeneratingObjects.inc


if EMPIRE_WARS


create_object BLANKOBJECT
{
  set_place_for_every_player
  number_of_objects 1
  second_object LUMBER_CAMP
  place_on_forest_zone 0
  find_closest
  actor_area 8
  actor_area_radius 4
  avoid_actor_area 94
  avoid_actor_area 40
 }

create_object VILLAGER
{
  set_place_for_every_player
  number_of_objects 4
  actor_area_to_place_in 8
  actor_area 71
  actor_area_radius 3
  avoid_forest_zone 0
  force_placement
 }

create_object BLANKOBJECT
{
  set_place_for_every_player
  number_of_objects 1
  place_on_forest_zone 0
  find_closest 
  actor_area 9
  actor_area_radius 4
  second_object LUMBER_CAMP
  avoid_actor_area 8
  avoid_actor_area 94
  avoid_actor_area 40
 }

create_object VILLAGER
{
  set_place_for_every_player
  number_of_objects 4  /* increased from 3 */
  actor_area_to_place_in 9
  actor_area 72
  actor_area_radius 3
  avoid_forest_zone 0
  force_placement
 }

create_object BLANKOBJECT
{
  set_place_for_every_player
  number_of_objects 1
  place_on_forest_zone 0
  avoid_actor_area 8
  avoid_actor_area 9
  avoid_actor_area 94
  avoid_actor_area 40
  find_closest 
  actor_area 99
  actor_area_radius 4
  second_object LUMBER_CAMP
 }

create_object VILLAGER
{
  set_place_for_every_player
  number_of_objects 4
  actor_area_to_place_in 99
  actor_area 73
  actor_area_radius 3
  avoid_forest_zone 0
  force_placement
 }

endif

/* hunt of 2x4 on forest line */

if INFINITE_RESOURCES
elseif DEATH_MATCH
else

create_object BLANKOBJECT 
{
  second_object HUNTABLE
if EMPIRE_WARS
  number_of_objects          4
else
  number_of_objects          8
endif
  number_of_groups	     1
  group_placement_radius     6
  set_loose_grouping
  set_gaia_object_only
  set_place_for_every_player
  max_distance_to_players    45
  find_closest
  avoid_forest_zone 0
  min_distance_group_placement 2
  actor_area 776
  actor_area_radius 3
  terrain_to_place_on LAYER_A
}

endif

/* vultures */

create_object DLC_VULTURE
{
   number_of_objects         2
   set_scaling_to_map_size
}

/* home straggle dead trees */

if EMPIRE_WARS
elseif DEATH_MATCH
elseif INFINITE_RESOURCES
else

create_object DLC_DEADTREE
{
  number_of_objects             2
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players       4
  max_distance_to_players       5
  min_distance_group_placement  2
  avoid_actor_area 14
}
create_object DLC_DEADTREE
{
  number_of_objects             3
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players       6
  max_distance_to_players       8
  min_distance_group_placement  2
  avoid_actor_area 14
}

endif


/* central eye candy */



if INFINITE_RESOURCES
else

create_object GOLD
{
   number_of_objects          3
   number_of_groups           9999
   temp_min_distance_group_placement  20
   group_placement_radius     2
   set_tight_grouping
   set_gaia_object_only
   avoid_forest_zone 30
   min_distance_to_players 30
   actor_area 66
   actor_area_radius 5
   min_distance_group_placement  3
}

endif

if INFINITE_RESOURCES
else

create_object STONE
{
   number_of_objects          3
   number_of_groups           9999
   temp_min_distance_group_placement  20
   group_placement_radius     2
   set_tight_grouping
   set_gaia_object_only
   avoid_forest_zone 30
   actor_area 67
   actor_area_radius 5
   avoid_actor_area 66
   min_distance_group_placement  3
}

endif


create_object RELIC
{
		if TINY_MAP
   number_of_objects            5
   temp_min_distance_group_placement 3
		elseif SMALL_MAP
   number_of_objects            5
   temp_min_distance_group_placement 5
		elseif MEDIUM_MAP
   number_of_objects            5
   temp_min_distance_group_placement 6
		elseif LARGE_MAP
   number_of_objects            7
   temp_min_distance_group_placement 6
		elseif HUGE_MAP
   number_of_objects            8
   temp_min_distance_group_placement 7
		elseif GIGANTIC_MAP
   number_of_objects            8
   temp_min_distance_group_placement 7
		elseif LUDIKRIS_MAP
   number_of_objects            999
   temp_min_distance_group_placement 25
		endif
   set_gaia_object_only
   terrain_to_place_on DIRT2
}

/* dead stragglers */

if DEATH_MATCH
elseif INFINITE_RESOURCES
else

create_object DLC_DEADTREE
{
  number_of_objects          rnd(3,6)
  number_of_groups	     80
  group_placement_radius     8
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players    20
  min_distance_group_placement 4
  terrain_to_place_on CRATER_LAYER_C 
  temp_min_distance_group_placement 18
}

endif

/* ****************************************************** */

<ELEVATION_GENERATION>

create_elevation        7
{
  base_terrain			LAYER_A
  number_of_clumps 		999999
  number_of_tiles               999999
  spacing 1
}

create_elevation        1
{
  base_terrain			CRATER_LAYER_C 

	if TINY_MAP
  number_of_clumps 30
  number_of_tiles 500
	elseif SMALL_MAP
  number_of_clumps 42
  number_of_tiles 720
	elseif MEDIUM_MAP
  number_of_clumps 60
  number_of_tiles 1000
	elseif LARGE_MAP
  number_of_clumps 84
  number_of_tiles 1385
	elseif HUGE_MAP
  number_of_clumps 100
  number_of_tiles 1680
	elseif GIGANTIC_MAP
  number_of_clumps 120
  number_of_tiles 2000
	else
  number_of_clumps 480
  number_of_tiles 8000
	endif

  spacing 1
  enable_balanced_elevation
}

/* ****************************************************** */
<CONNECTION_GENERATION>