/*

	Graupel by Forgotten Empires
	Created: 12/10/2024
	Updated: N/A
	
	Version: 1.0

*/

#const MAPSCALE_MODIFIER 1
#include_drs includes/scaling.inc

/* ----------------------------------- Debug Settings */

/* #define DEBUG_MODE */
/* #define TERRAIN_DEBUGGING */
/* #define EMPIRE_WARS_TESTING */
#define COLLAPSE

/* ----------------------------------- Generation Settings */

start_random
	percent_chance 50 #define NEARCTIC_TUNDRA
	percent_chance 50 #define ANTARCTIC_TUNDRA
end_random

/* #define CENTRALISE_SPAWN */
/* #define RESTRICTED_SPAWN */

/* ----------------------------------- Nomad Settings */

#define NOMAD_MAP
/* #define NOMAD_SCATTERED */
/* #define NOMAD_UNIT */
#define NOMAD_TREATY
/* #define NOMAD_HERDABLE */

/* ----------------------------------- Themes & Overwrites */

#define OVERWRITES
if OVERWRITES
endif

#define MAP_CONSTANTS
if MAP_CONSTANTS
	#const STRAGGLER_SPAWN 1249 /* Snowy Autumn Tree Tree */
	#const STRAGGLER_NEUTRAL 1249 /* Snowy Autumn Tree Tree */

	#const BEACH_TERRAIN 37
	
	#const SPAWN_FOREST 48
	#const SPAWN_TERRAIN 127

	#const BASE_FOREST 105
	#const BASE_FOREST_VARIATION_A 21
	#const BASE_FOREST_VARIATION_B 106

	#const BASE_TERRAIN 124
	#const BASE_BLEND_A 0
	#const BASE_BLEND_B 9
	#const BASE_BLEND_C 12
	#const BASE_BLEND_D 76

	#const POND_TERRAIN 35

	#const CAPTURE_TERRAIN 124
endif

#include_drs includes/themes.inc
#include_drs includes/constants.inc
#include_drs includes/decay.inc

<PLAYER_SETUP>
	#include_drs includes/preliminaries.inc
	#include_drs includes/gaia_civilisation.inc

	#define WATER_DEFAULT
	#include_drs includes/water_preset.inc
	water_definition WATER_PRESET

	random_placement
	ai_info_map_type NILE_DELTA 1 0

	if CONFINED_SETUP
		#const CONFINED_INCREASE 1.2
	else
		#const CONFINED_INCREASE 1
	endif
	
<LAND_GENERATION>
	base_terrain BASE_TERRAIN

	create_land
	{
		terrain_type WATER_SHALLOW
		land_percent 100
		base_size 2
		clumping_factor 15
		top_border 28
		left_border 28
		right_border 28
		bottom_border 28
		land_position 40 40
		land_id 500
	}

	create_land
	{
		terrain_type WATER_SHALLOW
		land_percent 100
		base_size 2
		clumping_factor 15
		top_border 28
		left_border 28
		right_border 28
		bottom_border 28
		land_position 60 40
		land_id 500
	}

	create_land
	{
		terrain_type WATER_SHALLOW
		land_percent 100
		base_size 2
		clumping_factor 15
		top_border 28
		left_border 28
		right_border 28
		bottom_border 28
		land_position 40 60
		land_id 500
	}

	create_land
	{
		terrain_type WATER_SHALLOW
		land_percent 100
		base_size 2
		clumping_factor 15
		top_border 28
		left_border 28
		right_border 28
		bottom_border 28
		land_position 60 60
		land_id 500
	}

	create_land
	{
		terrain_type SPAWN_TERRAIN
		land_percent 0
		base_size 0
		land_position 50 50
		land_id 420
	}

	create_player_lands
	{
		terrain_type SPAWN_TERRAIN
		land_percent 100
		base_size 2
		clumping_factor 15
		top_border 48
		left_border 48
		right_border 48
		bottom_border 48
		circle_radius 2 0
	}

<ELEVATION_GENERATION>
	create_elevation 5
	{
		enable_balanced_elevation
		base_terrain BASE_TERRAIN

		number_of_tiles (512 * MAPSCALE_AREA)
		number_of_clumps (32 * MAPSCALE_AREA)
	}

<CLIFF_GENERATION>
	#include_drs includes/cliffs.inc
	cliff_type CLIFF_TYPE

<TERRAIN_GENERATION>
	#include_drs includes/colour.inc
	color_correction COLOUR_MOOD
	
	create_terrain PLACEHOLDER_TERRAIN_A
	{
		base_terrain WATER_SHALLOW
		land_percent 100
		number_of_clumps 512
		set_scale_by_groups
		spacing_to_other_terrain_types 1
	}

	create_terrain SPAWN_TERRAIN
	{
		base_terrain WATER_SHALLOW
		number_of_tiles (256 * MAPSCALE_SIDE)
		number_of_clumps (128 * MAPSCALE_SIDE)
	}

	create_terrain WATER_SHALLOW { base_terrain PLACEHOLDER_TERRAIN_A land_percent 100 number_of_clumps 512 set_scale_by_groups }
	create_terrain WATER_SHALLOW { base_terrain PLACEHOLDER_TERRAIN_A land_percent 100 number_of_clumps 512 set_scale_by_groups }
	create_terrain WATER_SHALLOW { base_terrain PLACEHOLDER_TERRAIN_A land_percent 100 number_of_clumps 512 set_scale_by_groups }
	create_terrain WATER_SHALLOW { base_terrain PLACEHOLDER_TERRAIN_A land_percent 100 number_of_clumps 512 set_scale_by_groups }

	create_terrain PLACEHOLDER_TERRAIN_A
	{
		base_terrain WATER_SHALLOW
		land_percent 100
		number_of_clumps 512
		set_scale_by_groups
		spacing_to_other_terrain_types 2
	}

	create_terrain SPAWN_TERRAIN
	{
		base_terrain WATER_SHALLOW
		number_of_tiles (256 * MAPSCALE_SIDE)
		number_of_clumps (128 * MAPSCALE_SIDE)
	}

	create_terrain WATER_SHALLOW { base_terrain PLACEHOLDER_TERRAIN_A land_percent 100 number_of_clumps 512 set_scale_by_groups }
	create_terrain WATER_SHALLOW { base_terrain PLACEHOLDER_TERRAIN_A land_percent 100 number_of_clumps 512 set_scale_by_groups }
	create_terrain WATER_SHALLOW { base_terrain PLACEHOLDER_TERRAIN_A land_percent 100 number_of_clumps 512 set_scale_by_groups }
	create_terrain WATER_SHALLOW { base_terrain PLACEHOLDER_TERRAIN_A land_percent 100 number_of_clumps 512 set_scale_by_groups }

	create_terrain PLACEHOLDER_TERRAIN_A
	{
		base_terrain WATER_SHALLOW
		land_percent 100
		number_of_clumps 512
		set_scale_by_groups
		spacing_to_other_terrain_types 3
	}

	create_terrain SPAWN_TERRAIN
	{
		base_terrain WATER_SHALLOW
		number_of_tiles (256 * MAPSCALE_SIDE)
		number_of_clumps (128 * MAPSCALE_SIDE)
	}

	create_terrain WATER_SHALLOW { base_terrain PLACEHOLDER_TERRAIN_A land_percent 100 number_of_clumps 512 set_scale_by_groups }
	create_terrain WATER_SHALLOW { base_terrain PLACEHOLDER_TERRAIN_A land_percent 100 number_of_clumps 512 set_scale_by_groups }
	create_terrain WATER_SHALLOW { base_terrain PLACEHOLDER_TERRAIN_A land_percent 100 number_of_clumps 512 set_scale_by_groups }
	create_terrain WATER_SHALLOW { base_terrain PLACEHOLDER_TERRAIN_A land_percent 100 number_of_clumps 512 set_scale_by_groups }

	create_terrain PLACEHOLDER_TERRAIN_A
	{
		base_terrain WATER_SHALLOW
		land_percent 100
		number_of_clumps 512
		set_scale_by_groups
		spacing_to_other_terrain_types 4
	}

	create_terrain SPAWN_TERRAIN
	{
		base_terrain WATER_SHALLOW
		number_of_tiles (256 * MAPSCALE_SIDE)
		number_of_clumps (128 * MAPSCALE_SIDE)
	}

	create_terrain WATER_SHALLOW { base_terrain PLACEHOLDER_TERRAIN_A land_percent 100 number_of_clumps 512 set_scale_by_groups }
	create_terrain WATER_SHALLOW { base_terrain PLACEHOLDER_TERRAIN_A land_percent 100 number_of_clumps 512 set_scale_by_groups }
	create_terrain WATER_SHALLOW { base_terrain PLACEHOLDER_TERRAIN_A land_percent 100 number_of_clumps 512 set_scale_by_groups }
	create_terrain WATER_SHALLOW { base_terrain PLACEHOLDER_TERRAIN_A land_percent 100 number_of_clumps 512 set_scale_by_groups }

	create_terrain PLACEHOLDER_TERRAIN_A
	{
		base_terrain WATER_SHALLOW
		land_percent 100
		number_of_clumps 512
		set_scale_by_groups
		spacing_to_other_terrain_types 6
	}

	create_terrain SPAWN_TERRAIN
	{
		base_terrain WATER_SHALLOW
		number_of_tiles (256 * MAPSCALE_SIDE)
		number_of_clumps (128 * MAPSCALE_SIDE)
	}

	create_terrain WATER_SHALLOW { base_terrain PLACEHOLDER_TERRAIN_A land_percent 100 number_of_clumps 512 set_scale_by_groups }
	create_terrain WATER_SHALLOW { base_terrain PLACEHOLDER_TERRAIN_A land_percent 100 number_of_clumps 512 set_scale_by_groups }
	create_terrain WATER_SHALLOW { base_terrain PLACEHOLDER_TERRAIN_A land_percent 100 number_of_clumps 512 set_scale_by_groups }
	create_terrain WATER_SHALLOW { base_terrain PLACEHOLDER_TERRAIN_A land_percent 100 number_of_clumps 512 set_scale_by_groups }

	create_terrain BASE_FOREST
	{
		base_terrain BASE_TERRAIN
		land_percent 12
		number_of_clumps 24
		set_scale_by_groups
		spacing_to_other_terrain_types 2
	}

	create_terrain POND_TERRAIN
	{
		base_terrain BASE_TERRAIN
		land_percent 6
		number_of_clumps 32
		set_scale_by_groups
		spacing_to_other_terrain_types 2
		set_flat_terrain_only
	}

	create_terrain WATER_MEDIUM
	{
		base_terrain POND_TERRAIN
		number_of_tiles 80
		number_of_clumps 80
		set_scale_by_size
		set_scale_by_groups
	}

	create_terrain BASE_BLEND_A
	{
		base_terrain BASE_TERRAIN
		land_percent 6
		number_of_clumps 24
		set_scale_by_groups
		clumping_factor -10
		spacing_to_other_terrain_types 1
		terrain_mask 2
	}

	create_terrain BASE_BLEND_B
	{
		base_terrain BASE_TERRAIN
		land_percent 4
		number_of_clumps 16
		set_scale_by_groups
		clumping_factor -10
		spacing_to_other_terrain_types 1
		terrain_mask 2
	}

	create_terrain BASE_BLEND_C
	{
		base_terrain BASE_TERRAIN
		land_percent 2
		number_of_clumps 12
		set_scale_by_groups
		clumping_factor -10
		spacing_to_other_terrain_types 1
		terrain_mask 2
	}

	create_terrain BASE_BLEND_D
	{
		base_terrain BASE_TERRAIN
		land_percent 2
		number_of_clumps 8
		set_scale_by_groups
		clumping_factor -10
		spacing_to_other_terrain_types 1
		terrain_mask 2
	}

	create_terrain BASE_FOREST_VARIATION_A
	{
		base_terrain BASE_FOREST
		land_percent 4
		number_of_clumps 256
		set_scale_by_groups
	}

	create_terrain BASE_FOREST_VARIATION_B
	{
		base_terrain BASE_FOREST
		land_percent 2
		number_of_clumps 128
		set_scale_by_groups
	}

	create_terrain WATER_MEDIUM
	{
		base_terrain WATER_SHALLOW
		land_percent 2
		number_of_clumps 24
		set_scale_by_groups
		terrain_mask 1
	}

	create_terrain POWDER_LIGHT
	{
		base_terrain SPAWN_TERRAIN
		number_of_tiles (1024 * MAPSCALE_SIDE)
		number_of_clumps (128 * MAPSCALE_SIDE)
		terrain_mask 1
	}

	create_terrain POWDER_LIGHT
	{
		base_terrain BEACH_TERRAIN
		number_of_tiles (1024 * MAPSCALE_SIDE)
		number_of_clumps (128 * MAPSCALE_SIDE)
		terrain_mask 1
	}

<OBJECTS_GENERATION>
	if TERRAIN_DEBUGGING
	else
		#define PRELIMINARIES
		if PRELIMINARIES
			#const RESOURCE_SPACING_DEFAULT 12
			#const RESOURCE_SPACING_FAR 20
			#const RESOURCE_RESTRICTION 2

			#include_drs includes/object_setup.inc
			#include_drs includes/market_objects.inc

			create_object NERITIC_A
			{
				number_of_objects 2048
				set_scaling_to_map_size
				terrain_to_place_on WATER_MEDIUM

				actor_area 7000
				actor_area_radius 1
			}

			create_object PLACEHOLDER_GENERIC
			{
				number_of_objects 2048
				temp_min_distance_group_placement 1
				terrain_to_place_on SPAWN_TERRAIN

				actor_area 7001
				actor_area_radius 1
			}

			create_object PLACEHOLDER_GENERIC
			{
				number_of_objects 2048
				temp_min_distance_group_placement 1
				terrain_to_place_on WATER_SHALLOW

				actor_area 7002
				actor_area_radius 2
			}
		endif

		#include_drs includes/town_centres.inc

		#define VILLAGER_IGNORE_RESTRICTIONS
		#const VILLAGER_DISTANCE 0

		#include_drs includes/villagers.inc
		#include_drs includes/scouts.inc
		#include_drs includes/walls.inc
		
		#include_drs includes/defend_wonder.inc

		#define REGICIDE_SAFE

		#include_drs includes/regicide.inc

		#const CAPTURE_DISTANCE 18
		#const CAPTURE_ZONE_DISTANCE 8
		#const CAPTURE_ACTOR_AREA_RADIUS 24
		#define CAPTURE_RANDOM

		#include_drs includes/capture_relic.inc

		create_object TRANSPORT_SHIP
		{
			set_place_for_every_player
			min_distance_to_players 6
			set_circular_placement
			find_closest
			ignore_terrain_restrictions
			terrain_to_place_on WATER_SHALLOW

			actor_area 2600
			actor_area_radius 4

			avoid_actor_area 7001 /* Beach Terrain */
			avoid_actor_area 2600 /* Transport Ships */
		}

		if NOMAD_START
		else
			#include_drs includes/stragglers.inc
			#include_drs includes/starting_resources.inc

			if DEATH_MATCH
			elseif INFINITE_RESOURCES
			else
				#include_drs includes/herdable_starting.inc
				#include_drs includes/herdable.inc
				#include_drs includes/huntable.inc
				#include_drs includes/lureable.inc
				#include_drs includes/furbearers.inc
			endif
		endif

		if INFINITE_RESOURCES
		else
			create_object GOLD
			{
				number_of_objects 6
				number_of_groups 1024
				set_tight_grouping
				group_placement_radius 1
				min_distance_to_players 22
				set_circular_placement
				temp_min_distance_group_placement 24

				min_distance_group_placement 1
				max_distance_to_other_zones 8
				avoid_forest_zone 2
				avoid_cliff_zone 2
				min_distance_to_map_edge 4

				actor_area 1010
				actor_area_radius 2

				avoid_actor_area 20 /* Villagers */
				avoid_actor_area 21 /* Villagers, Additional A */
				avoid_actor_area 22 /* Villagers, Additional B */
				avoid_actor_area 30 /* Scout */
				avoid_actor_area 31 /* Scout, Secondary */
				avoid_actor_area 32 /* Scout, Tertiary */
				avoid_actor_area 50 /* Walls */
				avoid_actor_area 51 /* Walls, Second Layer */
				avoid_actor_area 52 /* Walls, Third Layer */
				avoid_actor_area 60 /* Straggler, Spawn */
				avoid_actor_area 61 /* Straggler, Escalation Mode */

				avoid_actor_area 100 /* Primary Forage Bush */
				avoid_actor_area 110 /* Secondary Forage Bush */
				avoid_actor_area 120 /* Tertiary Forage Bush */
				avoid_actor_area 130 /* Additional Forage Bush */
				avoid_actor_area 140 /* Custom Forage Bush */
				avoid_actor_area 150 /* Custom Forage Bush */
				avoid_actor_area 200 /* Primary Gold */
				avoid_actor_area 210 /* Secondary Gold */
				avoid_actor_area 220 /* Tertiary Gold */
				avoid_actor_area 230 /* Additional Gold */
				avoid_actor_area 240 /* Custom Gold */
				avoid_actor_area 250 /* Custom Gold */
				avoid_actor_area 300 /* Primary Stone */
				avoid_actor_area 310 /* Secondary Stone */
				avoid_actor_area 320 /* Tertiary Stone */
				avoid_actor_area 330 /* Additional Stone */
				avoid_actor_area 340 /* Custom Stone */
				avoid_actor_area 350 /* Custom Stone */

				avoid_actor_area 5100 /* King */
				avoid_actor_area 5110 /* Tower & Castle */
				avoid_actor_area 5400 /* Wonder */

				avoid_actor_area 10100 /* Default Primary Forage Bush - Spacer */
				avoid_actor_area 10200 /* Default Primary Gold - Spacer */
				avoid_actor_area 10300 /* Default Primary Stone - Spacer */

				avoid_actor_area 11000 /* Neutral Forage Bush - Spacer */
				avoid_actor_area 11001 /* Neutral Forage Bush, Additional A - Spacer */
				avoid_actor_area 11002 /* Neutral Forage Bush, Additional B - Spacer */
				avoid_actor_area 11010 /* Neutral Gold - Spacer */
				avoid_actor_area 11011 /* Neutral Gold, Additional A - Spacer */
				avoid_actor_area 11012 /* Neutral Gold, Additional B - Spacer */
				avoid_actor_area 11020 /* Neutral Stone - Spacer */
				avoid_actor_area 11021 /* Neutral Stone, Additional - Spacer A */
				avoid_actor_area 11022 /* Neutral Stone, Additional - Spacer B */
			}

			create_object PLACEHOLDER_GENERIC /* Neutral Gold - Spacer */
			{
				number_of_objects 1024
				temp_min_distance_group_placement 1
				actor_area_to_place_in 1010

				actor_area 11010
				actor_area_radius 6
			}

			create_object STONE
			{
				number_of_objects 4
				number_of_groups 1024
				set_tight_grouping
				group_placement_radius 1
				min_distance_to_players 22
				set_circular_placement
				temp_min_distance_group_placement 24

				min_distance_group_placement 2
				max_distance_to_other_zones 8
				avoid_forest_zone 2
				avoid_cliff_zone 2
				min_distance_to_map_edge 4

				actor_area 1020
				actor_area_radius 2

				avoid_actor_area 20 /* Villagers */
				avoid_actor_area 21 /* Villagers, Additional A */
				avoid_actor_area 22 /* Villagers, Additional B */
				avoid_actor_area 30 /* Scout */
				avoid_actor_area 31 /* Scout, Secondary */
				avoid_actor_area 32 /* Scout, Tertiary */
				avoid_actor_area 50 /* Walls */
				avoid_actor_area 51 /* Walls, Second Layer */
				avoid_actor_area 52 /* Walls, Third Layer */
				avoid_actor_area 60 /* Straggler, Spawn */
				avoid_actor_area 61 /* Straggler, Escalation Mode */

				avoid_actor_area 100 /* Primary Forage Bush */
				avoid_actor_area 110 /* Secondary Forage Bush */
				avoid_actor_area 120 /* Tertiary Forage Bush */
				avoid_actor_area 130 /* Additional Forage Bush */
				avoid_actor_area 140 /* Custom Forage Bush */
				avoid_actor_area 150 /* Custom Forage Bush */
				avoid_actor_area 200 /* Primary Gold */
				avoid_actor_area 210 /* Secondary Gold */
				avoid_actor_area 220 /* Tertiary Gold */
				avoid_actor_area 230 /* Additional Gold */
				avoid_actor_area 240 /* Custom Gold */
				avoid_actor_area 250 /* Custom Gold */
				avoid_actor_area 300 /* Primary Stone */
				avoid_actor_area 310 /* Secondary Stone */
				avoid_actor_area 320 /* Tertiary Stone */
				avoid_actor_area 330 /* Additional Stone */
				avoid_actor_area 340 /* Custom Stone */
				avoid_actor_area 350 /* Custom Stone */

				avoid_actor_area 5100 /* King */
				avoid_actor_area 5110 /* Tower & Castle */
				avoid_actor_area 5400 /* Wonder */

				avoid_actor_area 10100 /* Default Primary Forage Bush - Spacer */
				avoid_actor_area 10200 /* Default Primary Gold - Spacer */
				avoid_actor_area 10300 /* Default Primary Stone - Spacer */

				avoid_actor_area 11000 /* Neutral Forage Bush - Spacer */
				avoid_actor_area 11001 /* Neutral Forage Bush, Additional A - Spacer */
				avoid_actor_area 11002 /* Neutral Forage Bush, Additional B - Spacer */
				avoid_actor_area 11010 /* Neutral Gold - Spacer */
				avoid_actor_area 11011 /* Neutral Gold, Additional A - Spacer */
				avoid_actor_area 11012 /* Neutral Gold, Additional B - Spacer */
				avoid_actor_area 11020 /* Neutral Stone - Spacer */
				avoid_actor_area 11021 /* Neutral Stone, Additional - Spacer A */
				avoid_actor_area 11022 /* Neutral Stone, Additional - Spacer B */
			}

			create_object PLACEHOLDER_GENERIC /* Neutral Stone - Spacer */
			{
				number_of_objects 1024
				temp_min_distance_group_placement 1
				actor_area_to_place_in 1020

				actor_area 11020
				actor_area_radius 6
			}

			create_object HERDABLE_B
			{
				number_of_objects 2
				number_of_groups 1024
				set_loose_grouping
				group_placement_radius 2
				min_distance_to_players 22
				set_circular_placement
				temp_min_distance_group_placement 18

				min_distance_group_placement 1
				max_distance_to_other_zones 8
				avoid_forest_zone 2
				avoid_cliff_zone 2
				min_distance_to_map_edge 2

				actor_area 1035
				actor_area_radius 2

				avoid_actor_area 20 /* Villagers */
				avoid_actor_area 21 /* Villagers, Additional A */
				avoid_actor_area 22 /* Villagers, Additional B */
				avoid_actor_area 30 /* Scout */
				avoid_actor_area 31 /* Scout, Secondary */
				avoid_actor_area 32 /* Scout, Tertiary */
				avoid_actor_area 50 /* Walls */
				avoid_actor_area 51 /* Walls, Second Layer */
				avoid_actor_area 52 /* Walls, Third Layer */
				avoid_actor_area 60 /* Straggler, Spawn */
				avoid_actor_area 61 /* Straggler, Escalation Mode */

				avoid_actor_area 1000 /* Neutral Forage Bush */
				avoid_actor_area 1001 /* Neutral Forage Bush, Additional A */
				avoid_actor_area 1002 /* Neutral Forage Bush, Additional B */
				avoid_actor_area 1010 /* Neutral Gold */
				avoid_actor_area 1011 /* Neutral Gold, Additional A */
				avoid_actor_area 1012 /* Neutral Gold, Additional B */
				avoid_actor_area 1020 /* Neutral Stone */
				avoid_actor_area 1021 /* Neutral Stone, Additional A */
				avoid_actor_area 1022 /* Neutral Stone, Additional B */
				avoid_actor_area 1030 /* Neutral Small Herdable */
				avoid_actor_area 1031 /* Neutral Small Herdable, Additional A */
				avoid_actor_area 1032 /* Neutral Small Herdable, Additional B */
				avoid_actor_area 1035 /* Neutral Herdable */
				avoid_actor_area 1036 /* Neutral Herdable, Additional A */
				avoid_actor_area 1037 /* Neutral Herdable, Additional B */
				avoid_actor_area 1040 /* Neutral Small Huntable */
				avoid_actor_area 1041 /* Neutral Small Huntable, Additional A */
				avoid_actor_area 1042 /* Neutral Small Huntable, Additional B */
				avoid_actor_area 1045 /* Neutral Huntable */
				avoid_actor_area 1046 /* Neutral Huntable, Additional A */
				avoid_actor_area 1047 /* Neutral Huntable, Additional B */
				avoid_actor_area 1050 /* Neutral Lureable */
				avoid_actor_area 1051 /* Neutral Lureable, Additional A */
				avoid_actor_area 1052 /* Neutral Lureable, Additional B */
				avoid_actor_area 1060 /* Neutral Furbearers */
				avoid_actor_area 1061 /* Neutral Furbearers, Additional A */
				avoid_actor_area 1062 /* Neutral Furbearers, Additional B */

				avoid_actor_area 5100 /* King */
				avoid_actor_area 5110 /* Tower & Castle */
				avoid_actor_area 5400 /* Wonder */

				avoid_actor_area 10102 /* Minimum Primary Forage Bush - Spacer */
				avoid_actor_area 10202 /* Minimum Primary Gold - Spacer */
				avoid_actor_area 10302 /* Minimum Primary Stone - Spacer */
			}

			create_object HUNTABLE_SMALL_B
			{
				number_of_objects 4
				number_of_groups 1024
				set_loose_grouping
				group_placement_radius 2
				min_distance_to_players 22
				set_circular_placement
				temp_min_distance_group_placement 14

				min_distance_group_placement 1
				max_distance_to_other_zones 12
				avoid_forest_zone 2
				avoid_cliff_zone 2
				min_distance_to_map_edge 2

				actor_area 1040
				actor_area_radius 2

				avoid_actor_area 20 /* Villagers */
				avoid_actor_area 21 /* Villagers, Additional A */
				avoid_actor_area 22 /* Villagers, Additional B */
				avoid_actor_area 30 /* Scout */
				avoid_actor_area 31 /* Scout, Secondary */
				avoid_actor_area 32 /* Scout, Tertiary */
				avoid_actor_area 50 /* Walls */
				avoid_actor_area 51 /* Walls, Second Layer */
				avoid_actor_area 52 /* Walls, Third Layer */
				avoid_actor_area 60 /* Straggler, Spawn */
				avoid_actor_area 61 /* Straggler, Escalation Mode */

				avoid_actor_area 1000 /* Neutral Forage Bush */
				avoid_actor_area 1001 /* Neutral Forage Bush, Additional A */
				avoid_actor_area 1002 /* Neutral Forage Bush, Additional B */
				avoid_actor_area 1010 /* Neutral Gold */
				avoid_actor_area 1011 /* Neutral Gold, Additional A */
				avoid_actor_area 1012 /* Neutral Gold, Additional B */
				avoid_actor_area 1020 /* Neutral Stone */
				avoid_actor_area 1021 /* Neutral Stone, Additional A */
				avoid_actor_area 1022 /* Neutral Stone, Additional B */
				avoid_actor_area 1030 /* Neutral Small Herdable */
				avoid_actor_area 1031 /* Neutral Small Herdable, Additional A */
				avoid_actor_area 1032 /* Neutral Small Herdable, Additional B */
				avoid_actor_area 1035 /* Neutral Herdable */
				avoid_actor_area 1036 /* Neutral Herdable, Additional A */
				avoid_actor_area 1037 /* Neutral Herdable, Additional B */
				avoid_actor_area 1040 /* Neutral Small Huntable */
				avoid_actor_area 1041 /* Neutral Small Huntable, Additional A */
				avoid_actor_area 1042 /* Neutral Small Huntable, Additional B */
				avoid_actor_area 1045 /* Neutral Huntable */
				avoid_actor_area 1046 /* Neutral Huntable, Additional A */
				avoid_actor_area 1047 /* Neutral Huntable, Additional B */
				avoid_actor_area 1050 /* Neutral Lureable */
				avoid_actor_area 1051 /* Neutral Lureable, Additional A */
				avoid_actor_area 1052 /* Neutral Lureable, Additional B */
				avoid_actor_area 1060 /* Neutral Furbearers */
				avoid_actor_area 1061 /* Neutral Furbearers, Additional A */
				avoid_actor_area 1062 /* Neutral Furbearers, Additional B */

				avoid_actor_area 5100 /* King */
				avoid_actor_area 5110 /* Tower & Castle */
				avoid_actor_area 5400 /* Wonder */

				avoid_actor_area 10102 /* Minimum Primary Forage Bush - Spacer */
				avoid_actor_area 10202 /* Minimum Primary Gold - Spacer */
				avoid_actor_area 10302 /* Minimum Primary Stone - Spacer */

				avoid_actor_area 11000 /* Neutral Forage Bush - Spacer */
				avoid_actor_area 11001 /* Neutral Forage Bush, Additional A - Spacer */
				avoid_actor_area 11002 /* Neutral Forage Bush, Additional B - Spacer */
			}

			create_object LUREABLE_B
			{
				number_of_objects 1
				number_of_groups 1024
				set_loose_grouping
				group_placement_radius 2
				min_distance_to_players 22
				set_circular_placement
				temp_min_distance_group_placement 24

				min_distance_group_placement 1
				max_distance_to_other_zones 12
				avoid_forest_zone 2
				avoid_cliff_zone 2
				min_distance_to_map_edge 2

				actor_area 1050
				actor_area_radius 2

				avoid_actor_area 20 /* Villagers */
				avoid_actor_area 21 /* Villagers, Additional A */
				avoid_actor_area 22 /* Villagers, Additional B */
				avoid_actor_area 30 /* Scout */
				avoid_actor_area 31 /* Scout, Secondary */
				avoid_actor_area 32 /* Scout, Tertiary */
				avoid_actor_area 50 /* Walls */
				avoid_actor_area 51 /* Walls, Second Layer */
				avoid_actor_area 52 /* Walls, Third Layer */
				avoid_actor_area 60 /* Straggler, Spawn */
				avoid_actor_area 61 /* Straggler, Escalation Mode */

				avoid_actor_area 1000 /* Neutral Forage Bush */
				avoid_actor_area 1001 /* Neutral Forage Bush, Additional A */
				avoid_actor_area 1002 /* Neutral Forage Bush, Additional B */
				avoid_actor_area 1010 /* Neutral Gold */
				avoid_actor_area 1011 /* Neutral Gold, Additional A */
				avoid_actor_area 1012 /* Neutral Gold, Additional B */
				avoid_actor_area 1020 /* Neutral Stone */
				avoid_actor_area 1021 /* Neutral Stone, Additional A */
				avoid_actor_area 1022 /* Neutral Stone, Additional B */
				avoid_actor_area 1030 /* Neutral Small Herdable */
				avoid_actor_area 1031 /* Neutral Small Herdable, Additional A */
				avoid_actor_area 1032 /* Neutral Small Herdable, Additional B */
				avoid_actor_area 1035 /* Neutral Herdable */
				avoid_actor_area 1036 /* Neutral Herdable, Additional A */
				avoid_actor_area 1037 /* Neutral Herdable, Additional B */
				avoid_actor_area 1040 /* Neutral Small Huntable */
				avoid_actor_area 1041 /* Neutral Small Huntable, Additional A */
				avoid_actor_area 1042 /* Neutral Small Huntable, Additional B */
				avoid_actor_area 1045 /* Neutral Huntable */
				avoid_actor_area 1046 /* Neutral Huntable, Additional A */
				avoid_actor_area 1047 /* Neutral Huntable, Additional B */
				avoid_actor_area 1050 /* Neutral Lureable */
				avoid_actor_area 1051 /* Neutral Lureable, Additional A */
				avoid_actor_area 1052 /* Neutral Lureable, Additional B */
				avoid_actor_area 1060 /* Neutral Furbearers */
				avoid_actor_area 1061 /* Neutral Furbearers, Additional A */
				avoid_actor_area 1062 /* Neutral Furbearers, Additional B */

				avoid_actor_area 5100 /* King */
				avoid_actor_area 5110 /* Tower & Castle */
				avoid_actor_area 5400 /* Wonder */

				avoid_actor_area 10102 /* Minimum Primary Forage Bush - Spacer */
				avoid_actor_area 10202 /* Minimum Primary Gold - Spacer */
				avoid_actor_area 10302 /* Minimum Primary Stone - Spacer */
			}

			create_object FURBEARER_B
			{
				number_of_objects 2
				number_of_groups 1024
				set_loose_grouping
				group_placement_radius 2
				min_distance_to_players 22
				set_circular_placement
				temp_min_distance_group_placement 20

				min_distance_group_placement 1
				max_distance_to_other_zones 16
				avoid_forest_zone 2
				avoid_cliff_zone 2
				min_distance_to_map_edge 2

				actor_area 1060
				actor_area_radius 2

				avoid_actor_area 20 /* Villagers */
				avoid_actor_area 21 /* Villagers, Additional A */
				avoid_actor_area 22 /* Villagers, Additional B */
				avoid_actor_area 30 /* Scout */
				avoid_actor_area 31 /* Scout, Secondary */
				avoid_actor_area 32 /* Scout, Tertiary */
				avoid_actor_area 50 /* Walls */
				avoid_actor_area 51 /* Walls, Second Layer */
				avoid_actor_area 52 /* Walls, Third Layer */
				avoid_actor_area 60 /* Straggler, Spawn */
				avoid_actor_area 61 /* Straggler, Escalation Mode */

				avoid_actor_area 1000 /* Neutral Forage Bush */
				avoid_actor_area 1001 /* Neutral Forage Bush, Additional A */
				avoid_actor_area 1002 /* Neutral Forage Bush, Additional B */
				avoid_actor_area 1010 /* Neutral Gold */
				avoid_actor_area 1011 /* Neutral Gold, Additional A */
				avoid_actor_area 1012 /* Neutral Gold, Additional B */
				avoid_actor_area 1020 /* Neutral Stone */
				avoid_actor_area 1021 /* Neutral Stone, Additional A */
				avoid_actor_area 1022 /* Neutral Stone, Additional B */
				avoid_actor_area 1030 /* Neutral Small Herdable */
				avoid_actor_area 1031 /* Neutral Small Herdable, Additional A */
				avoid_actor_area 1032 /* Neutral Small Herdable, Additional B */
				avoid_actor_area 1035 /* Neutral Herdable */
				avoid_actor_area 1036 /* Neutral Herdable, Additional A */
				avoid_actor_area 1037 /* Neutral Herdable, Additional B */
				avoid_actor_area 1040 /* Neutral Small Huntable */
				avoid_actor_area 1041 /* Neutral Small Huntable, Additional A */
				avoid_actor_area 1042 /* Neutral Small Huntable, Additional B */
				avoid_actor_area 1045 /* Neutral Huntable */
				avoid_actor_area 1046 /* Neutral Huntable, Additional A */
				avoid_actor_area 1047 /* Neutral Huntable, Additional B */
				avoid_actor_area 1050 /* Neutral Lureable */
				avoid_actor_area 1051 /* Neutral Lureable, Additional A */
				avoid_actor_area 1052 /* Neutral Lureable, Additional B */
				avoid_actor_area 1060 /* Neutral Furbearers */
				avoid_actor_area 1061 /* Neutral Furbearers, Additional A */
				avoid_actor_area 1062 /* Neutral Furbearers, Additional B */

				avoid_actor_area 5100 /* King */
				avoid_actor_area 5110 /* Tower & Castle */
				avoid_actor_area 5400 /* Wonder */

				avoid_actor_area 10102 /* Minimum Primary Forage Bush - Spacer */
				avoid_actor_area 10202 /* Minimum Primary Gold - Spacer */
				avoid_actor_area 10302 /* Minimum Primary Stone - Spacer */
			}
		endif

		if DEATH_MATCH
		else
			#const PREDATOR_GROUPS 0

			#include_drs includes/predators.inc
			#include_drs includes/riparian.inc
		endif

		#include_drs includes/neritic.inc
		#include_drs includes/oysters.inc

		#const FRESHWATER_SPACING 12

		#include_drs includes/aquatic_freshwater.inc

		#const SALTWATER_COUNT 0

		#include_drs includes/aquatic_saltwater.inc

		if DEATH_MATCH
		else
			#const STRAGGLER_GROUPS 16

			#include_drs includes/stragglers_neutral.inc
			#include_drs includes/stragglers_coastal.inc
			#include_drs includes/reeds.inc
		endif

		#define RELIC_TYPE_UNRESTRICTED

		#include_drs includes/relics.inc

		#define EXTRA_STUFF
		if EXTRA_STUFF
			if EMPIRE_WARS_MODE
				#include_drs includes/empire_wars.inc
				#include_drs includes/empire_food.inc
				#include_drs includes/empire_gold.inc
				#include_drs includes/empire_wood.inc
				#include_drs includes/empire_buildings.inc
			elseif ESCALATION_MODE
				#include_drs includes/escalation_mode.inc
			endif

			#define AESTHETICS
			if AESTHETICS
				#include_drs includes/birds.inc

				create_object STUMP
				{
					number_of_objects (48 * MAPSCALE_AREA)
					temp_min_distance_group_placement 1
					place_on_forest_zone
					terrain_to_place_on BASE_TERRAIN

					avoid_actor_area 1300 /* Stragglers */
				}

				create_object BARRELS
				{
					set_facet 4
					number_of_objects 1024
					temp_min_distance_group_placement 16
					actor_area_to_place_in 7000 /* Neutral Neritic */
				}

				create_object PLANT_DEAD
				{
					number_of_objects (32 * MAPSCALE_AREA)
					temp_min_distance_group_placement 2
					
					max_distance_to_other_zones 4
				}

				create_object SEA_ROCKS_A
				{
					number_of_objects 1024
					temp_min_distance_group_placement 32
					terrain_to_place_on WATER_SHALLOW
					
					max_distance_to_other_zones 2
					
					actor_area 8000
					actor_area_radius 4
				}

				create_object SEA_ROCKS_B
				{
					number_of_objects 1024
					temp_min_distance_group_placement 24
					terrain_to_place_on WATER_SHALLOW
					
					max_distance_to_other_zones 2
					
					actor_area 8000
					actor_area_radius 4

					avoid_actor_area 8000 /* Sea Rocks */
				}

				create_object SKELETON
				{
					place_on_specific_land_id 420
					number_of_objects 3
					set_scaling_to_map_size
					temp_min_distance_group_placement 1
					
					max_distance_to_other_zones 1
					
					actor_area 8001
					actor_area_radius 0

					avoid_actor_area 20 /* Villagers */
					avoid_actor_area 8001 /* Skeletons */
				}
			endif
		endif
	endif