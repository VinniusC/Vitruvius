/*

	Yucatan by Forgotten Empires

	Created: 11/09/2000
	Updated: 09/10/2024
	
	Version: 2.0

*/

#const MAPSCALE_MODIFIER 1
#include_drs includes/scaling.inc

/* ----------------------------------- Debug Settings */

/* #define DEBUG_MODE */
/* #define TERRAIN_DEBUGGING */
/* #define EMPIRE_WARS_TESTING */
#define COLLAPSE

/* ----------------------------------- Generation Settings */

#define NEOTROPICAL_TROPICAL

/* #define CENTRALISE_SPAWN */
/* #define RESTRICTED_SPAWN */

/* ----------------------------------- Nomad Settings */

/* #define NOMAD_MAP */
/* #define NOMAD_SCATTERED */
/* #define NOMAD_UNIT */
#define NOMAD_TREATY
/* #define NOMAD_HERDABLE */

/* ----------------------------------- Themes & Overwrites */

#define OVERWRITES
if OVERWRITES
endif

#define MAP_CONSTANTS
if MAP_CONSTANTS
	if NEOTROPICAL_TROPICAL
		#const STRAGGLER_SPAWN 302
		#const STRAGGLER_NEUTRAL 302

		#const BEACH_TERRAIN 52

		#const SPAWN_FOREST 17
		#const SPAWN_FOREST_VARIATION_A 56
		#const SPAWN_FOREST_VARIATION_B 89

		#const SPAWN_TERRAIN 60
		#const SPAWN_BLEND_A 77
		#const SPAWN_BLEND_B 5
		#const SPAWN_BLEND_C 76
		#const SPAWN_BLEND_D 3
		#const SPAWN_BLEND_E 0
		#const SPAWN_BLEND_F 75

		#const BASE_FOREST 17
		#const BASE_FOREST_VARIATION 56

		#const BASE_TERRAIN 60
		#const BASE_BLEND_A 6
		#const BASE_BLEND_B 42
		#const BASE_BLEND_C 3
		#const BASE_BLEND_D 0

		#const COASTAL_TERRAIN 14
	endif
endif

#include_drs includes/themes.inc
#include_drs includes/constants.inc
#include_drs includes/decay.inc

<PLAYER_SETUP>
	#include_drs includes/preliminaries.inc
	#include_drs includes/gaia_civilisation.inc

	#define WATER_POND
	#include_drs includes/water_preset.inc
	water_definition WATER_PRESET

	random_placement
	ai_info_map_type YUCATAN 0 0

	if CONFINED_SETUP
		#const CONFINED_INCREASE 1.2
	else
		#const CONFINED_INCREASE 1
	endif

<LAND_GENERATION>
	base_terrain WATER_SHALLOW

	create_land
	{
		terrain_type SPAWN_TERRAIN
		number_of_tiles 0
		base_size 0
		land_position 50 50
		land_id 420
	}

	create_player_lands
	{
		terrain_type SPAWN_TERRAIN
		land_percent 100
		base_size 12
		clumping_factor 15
		other_zone_avoidance_distance 4
		circle_radius 36 8
	}

<ELEVATION_GENERATION>
	create_elevation 5
	{
		enable_balanced_elevation
		base_terrain SPAWN_TERRAIN

		#const SPAWN_ELEVATION_TILES 768
		#const SPAWN_ELEVATION_CLUMPS 48

		number_of_tiles (SPAWN_ELEVATION_TILES * MAPSCALE_AREA)
		number_of_clumps (SPAWN_ELEVATION_CLUMPS * MAPSCALE_AREA)
	}

<CLIFF_GENERATION>
	#include_drs includes/cliffs.inc
	cliff_type CLIFF_TYPE

<TERRAIN_GENERATION>
	#include_drs includes/colour.inc
	color_correction COLOUR_MOOD



	create_terrain BASE_TERRAIN
	{
		base_terrain SPAWN_TERRAIN
		land_percent 100
		number_of_clumps 512
		set_scale_by_groups
		beach_terrain BEACH_TERRAIN
	}
	
	create_terrain SPAWN_FOREST
	{
		base_terrain SPAWN_TERRAIN
		land_percent 20
		number_of_clumps 24
		set_scale_by_groups
		set_avoid_player_start_areas 16
		spacing_to_other_terrain_types 4
		beach_terrain BEACH_TERRAIN
	}

	create_terrain SPAWN_FOREST
	{
		base_terrain SPAWN_TERRAIN
		land_percent 4
		number_of_clumps 20
		set_scale_by_groups
		set_avoid_player_start_areas 26
		spacing_to_other_terrain_types 2
		beach_terrain BEACH_TERRAIN
	}

	create_terrain SPAWN_FOREST_VARIATION_A
	{
		base_terrain SPAWN_FOREST
		land_percent 100
		number_of_clumps 512
		set_scale_by_groups
		spacing_to_other_terrain_types 1
		beach_terrain BEACH_TERRAIN
	}

	create_terrain SPAWN_FOREST_VARIATION_B
	{
		base_terrain SPAWN_FOREST
		land_percent 2
		number_of_clumps 256
		set_scale_by_groups
		beach_terrain BEACH_TERRAIN
	}

	create_terrain SPAWN_FOREST
	{
		base_terrain SPAWN_FOREST_VARIATION_A
		land_percent 1
		number_of_clumps 128
		set_scale_by_groups
		beach_terrain BEACH_TERRAIN
	}

	create_terrain SPAWN_TERRAIN
	{
		base_terrain WATER_SHALLOW

		if 2_PLAYER_GAME
			number_of_tiles (196 * PLAYER_COUNT)
		else
			number_of_tiles (314 * PLAYER_COUNT)
		endif

		number_of_clumps (5 * PLAYER_COUNT)
		beach_terrain BEACH_TERRAIN
	}

	create_terrain SHALLOW
	{
		base_terrain WATER_SHALLOW
		number_of_tiles 88
		number_of_clumps 42
		set_scale_by_groups
		terrain_mask 2
		beach_terrain BEACH_TERRAIN
	}

	create_terrain WATER_MEDIUM
	{
		base_terrain WATER_SHALLOW
		number_of_tiles 24
		number_of_clumps 12
		set_scale_by_groups
		terrain_mask 1
		beach_terrain BEACH_TERRAIN
	}

	create_terrain WATER_DEEP
	{
		base_terrain WATER_SHALLOW
		number_of_tiles 24
		number_of_clumps 12
		set_scale_by_groups
		spacing_to_other_terrain_types 1
		terrain_mask 1
		beach_terrain BEACH_TERRAIN
	}

	create_terrain SHALLOWS_MANGROVE
	{
		base_terrain WATER_SHALLOW
		number_of_tiles 128
		number_of_clumps 48
		set_scale_by_groups
		terrain_mask 1
		beach_terrain BEACH_TERRAIN
	}

	create_terrain WATER_AZURE
	{
		base_terrain WATER_SHALLOW
		number_of_tiles 24
		number_of_clumps 12
		set_scale_by_groups
		terrain_mask 1
		beach_terrain BEACH_TERRAIN
	}

	create_terrain SPAWN_BLEND_A
	{
		base_terrain SPAWN_TERRAIN
		land_percent 8
		number_of_clumps 16
		set_scale_by_groups
		terrain_mask 2
		beach_terrain BEACH_TERRAIN
	}

	create_terrain SPAWN_BLEND_A
	{
		base_terrain SPAWN_TERRAIN
		land_percent 4
		number_of_clumps 24
		set_scale_by_groups
		terrain_mask 2
		beach_terrain BEACH_TERRAIN
	}

	create_terrain SPAWN_BLEND_B
	{
		base_terrain SPAWN_TERRAIN
		land_percent 8
		number_of_clumps 24
		clumping_factor -10
		set_scale_by_groups
		terrain_mask 1
		beach_terrain BEACH_TERRAIN
	}

	create_terrain SPAWN_BLEND_C
	{
		base_terrain SPAWN_TERRAIN
		land_percent 12
		number_of_clumps 24
		set_scale_by_groups
		clumping_factor -10
		terrain_mask 1
		beach_terrain BEACH_TERRAIN
	}

	create_terrain SPAWN_BLEND_D
	{
		base_terrain SPAWN_TERRAIN
		land_percent 4
		number_of_clumps 24
		set_scale_by_groups
		terrain_mask 1
		beach_terrain BEACH_TERRAIN
	}

	create_terrain SPAWN_BLEND_E
	{
		base_terrain SPAWN_TERRAIN
		land_percent 4
		number_of_clumps 24
		set_scale_by_groups
		terrain_mask 1
		beach_terrain BEACH_TERRAIN
	}

	create_terrain SPAWN_BLEND_F
	{
		base_terrain SPAWN_TERRAIN
		land_percent 2
		number_of_clumps 12
		set_scale_by_groups
		clumping_factor -5
		spacing_to_other_terrain_types 2
		terrain_mask 2
		beach_terrain BEACH_TERRAIN
	}

	create_terrain SPAWN_BLEND_F
	{
		base_terrain SPAWN_TERRAIN
		land_percent 2
		number_of_clumps 12
		set_scale_by_groups
		clumping_factor -5
		spacing_to_other_terrain_types 2
		terrain_mask 2
		beach_terrain BEACH_TERRAIN
	}

<OBJECTS_GENERATION>
	if TERRAIN_DEBUGGING
	else
		#define PRELIMINARIES
		if PRELIMINARIES
			#const RESOURCE_SPACING_DEFAULT 6
			#const RESOURCE_SPACING_FAR 10
			#const RESOURCE_RESTRICTION 1

			#include_drs includes/object_setup.inc
			#include_drs includes/market_objects.inc

			create_object PLACEHOLDER_GENERIC
			{
				number_of_objects 1024
				temp_min_distance_group_placement 1
				terrain_to_place_on SPAWN_FOREST

				actor_area 7000
				actor_area_radius 2
			}

			create_object PLACEHOLDER_GENERIC
			{
				number_of_objects 1024
				temp_min_distance_group_placement 1
				terrain_to_place_on SPAWN_FOREST_VARIATION_A

				actor_area 7000
				actor_area_radius 2
			}

			create_object PLACEHOLDER_GENERIC
			{
				number_of_objects 1024
				temp_min_distance_group_placement 1
				terrain_to_place_on SPAWN_FOREST_VARIATION_B

				actor_area 7000
				actor_area_radius 2
			}
		endif

		#include_drs includes/town_centres.inc
		#include_drs includes/villagers.inc
		#include_drs includes/scouts.inc
		#include_drs includes/walls.inc

		#include_drs includes/defend_wonder.inc
		#include_drs includes/regicide.inc
		#include_drs includes/capture_relic.inc

		if NOMAD_START
		else
			#include_drs includes/stragglers.inc

			#define FORAGE_BUSH_PRIMARY

			#const FORAGE_BUSH_SECONDARY_DISTANCE 12
			#define FORAGE_BUSH_SECONDARY

			#define GOLD_PRIMARY

			#const GOLD_SECONDARY_DISTANCE 16
			#define GOLD_SECONDARY

			#const GOLD_TERTIARY_DISTANCE 18
			#define GOLD_TERTIARY

			#const STONE_PRIMARY_DISTANCE 14
			#define STONE_PRIMARY

			#const STONE_SECONDARY_DISTANCE 18
			#define STONE_SECONDARY

			#include_drs includes/starting_resources.inc

			if DEATH_MATCH
			elseif INFINITE_RESOURCES
			else
				#define HERDABLE_STARTING_COUNT_SIX

				#include_drs includes/herdable_starting.inc

				#const HERDABLE_COUNT 3
				#const HERDABLE_GROUPS 3

				#include_drs includes/herdable.inc

				#const HUNTABLE_COUNT 4
				#const HUNTABLE_GROUPS 2
				#const HUNTABLE_DISTANCE 16

				#include_drs includes/huntable.inc

				#const LUREABLE_COUNT 3
				#const LUREABLE_ADDITIONAL_COUNT 2

				#include_drs includes/lureable.inc
				#include_drs includes/furbearers.inc
			endif
		endif

		if INFINITE_RESOURCES
		else
			create_object FORAGE_BUSH
			{
				second_object PLANT_DEAD
				number_of_objects 6
				number_of_groups 1024
				set_tight_grouping
				group_placement_radius 1
				min_distance_to_players 52
				set_circular_placement
				temp_min_distance_group_placement 24

				min_distance_group_placement 1
				max_distance_to_other_zones 8
				avoid_forest_zone 2
				avoid_cliff_zone 2
				min_distance_to_map_edge 6

				actor_area 1000
				actor_area_radius 2

				avoid_actor_area 20 /* Villagers */
				avoid_actor_area 21 /* Villagers, Additional A */
				avoid_actor_area 22 /* Villagers, Additional B */
				avoid_actor_area 30 /* Scout */
				avoid_actor_area 31 /* Scout, Secondary */
				avoid_actor_area 32 /* Scout, Tertiary */
				avoid_actor_area 50 /* Walls */
				avoid_actor_area 51 /* Walls, Second Layer */
				avoid_actor_area 52 /* Walls, Third Layer */
				avoid_actor_area 60 /* Straggler, Spawn */
				avoid_actor_area 61 /* Straggler, Escalation Mode */

				avoid_actor_area 100 /* Primary Forage Bush */
				avoid_actor_area 110 /* Secondary Forage Bush */
				avoid_actor_area 120 /* Tertiary Forage Bush */
				avoid_actor_area 130 /* Additional Forage Bush */
				avoid_actor_area 140 /* Custom Forage Bush */
				avoid_actor_area 150 /* Custom Forage Bush */
				avoid_actor_area 200 /* Primary Gold */
				avoid_actor_area 210 /* Secondary Gold */
				avoid_actor_area 220 /* Tertiary Gold */
				avoid_actor_area 230 /* Additional Gold */
				avoid_actor_area 240 /* Custom Gold */
				avoid_actor_area 250 /* Custom Gold */
				avoid_actor_area 300 /* Primary Stone */
				avoid_actor_area 310 /* Secondary Stone */
				avoid_actor_area 320 /* Tertiary Stone */
				avoid_actor_area 330 /* Additional Stone */
				avoid_actor_area 340 /* Custom Stone */
				avoid_actor_area 350 /* Custom Stone */

				avoid_actor_area 5100 /* King */
				avoid_actor_area 5110 /* Tower & Castle */
				avoid_actor_area 5350 /* Monastery */
				avoid_actor_area 5400 /* Wonder */

				avoid_actor_area 10100 /* Default Primary Forage Bush - Spacer */
				avoid_actor_area 10200 /* Default Primary Gold - Spacer */
				avoid_actor_area 10300 /* Default Primary Stone - Spacer */

				avoid_actor_area 11000 /* Neutral Forage Bush - Spacer */
				avoid_actor_area 11001 /* Neutral Forage Bush, Additional A - Spacer */
				avoid_actor_area 11002 /* Neutral Forage Bush, Additional B - Spacer */
				avoid_actor_area 11010 /* Neutral Gold - Spacer */
				avoid_actor_area 11011 /* Neutral Gold, Additional A - Spacer */
				avoid_actor_area 11012 /* Neutral Gold, Additional B - Spacer */
				avoid_actor_area 11020 /* Neutral Stone - Spacer */
				avoid_actor_area 11021 /* Neutral Stone, Additional - Spacer A */
				avoid_actor_area 11022 /* Neutral Stone, Additional - Spacer B */
			}

			create_object PLACEHOLDER_GENERIC /* Neutral Forage Bush - Spacer */
			{
				number_of_objects 1024
				temp_min_distance_group_placement 1
				actor_area_to_place_in 1000

				actor_area 11000
				actor_area_radius 6
			}

			create_object GOLD
			{
				number_of_objects 6
				number_of_groups 1024
				set_tight_grouping
				group_placement_radius 1
				min_distance_to_players 26
				set_circular_placement
				temp_min_distance_group_placement 24

				min_distance_group_placement 1
				max_distance_to_other_zones 8
				avoid_forest_zone 2
				avoid_cliff_zone 2
				min_distance_to_map_edge 6

				actor_area 1010
				actor_area_radius 2

				avoid_actor_area 20 /* Villagers */
				avoid_actor_area 21 /* Villagers, Additional A */
				avoid_actor_area 22 /* Villagers, Additional B */
				avoid_actor_area 30 /* Scout */
				avoid_actor_area 31 /* Scout, Secondary */
				avoid_actor_area 32 /* Scout, Tertiary */
				avoid_actor_area 50 /* Walls */
				avoid_actor_area 51 /* Walls, Second Layer */
				avoid_actor_area 52 /* Walls, Third Layer */
				avoid_actor_area 60 /* Straggler, Spawn */
				avoid_actor_area 61 /* Straggler, Escalation Mode */

				avoid_actor_area 100 /* Primary Forage Bush */
				avoid_actor_area 110 /* Secondary Forage Bush */
				avoid_actor_area 120 /* Tertiary Forage Bush */
				avoid_actor_area 130 /* Additional Forage Bush */
				avoid_actor_area 140 /* Custom Forage Bush */
				avoid_actor_area 150 /* Custom Forage Bush */
				avoid_actor_area 200 /* Primary Gold */
				avoid_actor_area 210 /* Secondary Gold */
				avoid_actor_area 220 /* Tertiary Gold */
				avoid_actor_area 230 /* Additional Gold */
				avoid_actor_area 240 /* Custom Gold */
				avoid_actor_area 250 /* Custom Gold */
				avoid_actor_area 300 /* Primary Stone */
				avoid_actor_area 310 /* Secondary Stone */
				avoid_actor_area 320 /* Tertiary Stone */
				avoid_actor_area 330 /* Additional Stone */
				avoid_actor_area 340 /* Custom Stone */
				avoid_actor_area 350 /* Custom Stone */

				avoid_actor_area 5100 /* King */
				avoid_actor_area 5110 /* Tower & Castle */
				avoid_actor_area 5350 /* Monastery */
				avoid_actor_area 5400 /* Wonder */

				avoid_actor_area 10100 /* Default Primary Forage Bush - Spacer */
				avoid_actor_area 10200 /* Default Primary Gold - Spacer */
				avoid_actor_area 10300 /* Default Primary Stone - Spacer */

				avoid_actor_area 11000 /* Neutral Forage Bush - Spacer */
				avoid_actor_area 11001 /* Neutral Forage Bush, Additional A - Spacer */
				avoid_actor_area 11002 /* Neutral Forage Bush, Additional B - Spacer */
				avoid_actor_area 11010 /* Neutral Gold - Spacer */
				avoid_actor_area 11011 /* Neutral Gold, Additional A - Spacer */
				avoid_actor_area 11012 /* Neutral Gold, Additional B - Spacer */
				avoid_actor_area 11020 /* Neutral Stone - Spacer */
				avoid_actor_area 11021 /* Neutral Stone, Additional - Spacer A */
				avoid_actor_area 11022 /* Neutral Stone, Additional - Spacer B */
			}

			create_object PLACEHOLDER_GENERIC /* Neutral Gold - Spacer */
			{
				number_of_objects 1024
				temp_min_distance_group_placement 1
				actor_area_to_place_in 1010

				actor_area 11010
				actor_area_radius 6
			}

			create_object STONE
			{
				number_of_objects 6
				number_of_groups 1024
				set_tight_grouping
				group_placement_radius 1
				min_distance_to_players 26
				set_circular_placement
				temp_min_distance_group_placement 24

				min_distance_group_placement 1
				max_distance_to_other_zones 8
				avoid_forest_zone 2
				avoid_cliff_zone 2
				min_distance_to_map_edge 6

				actor_area 1020
				actor_area_radius 2

				avoid_actor_area 20 /* Villagers */
				avoid_actor_area 21 /* Villagers, Additional A */
				avoid_actor_area 22 /* Villagers, Additional B */
				avoid_actor_area 30 /* Scout */
				avoid_actor_area 31 /* Scout, Secondary */
				avoid_actor_area 32 /* Scout, Tertiary */
				avoid_actor_area 50 /* Walls */
				avoid_actor_area 51 /* Walls, Second Layer */
				avoid_actor_area 52 /* Walls, Third Layer */
				avoid_actor_area 60 /* Straggler, Spawn */
				avoid_actor_area 61 /* Straggler, Escalation Mode */

				avoid_actor_area 100 /* Primary Forage Bush */
				avoid_actor_area 110 /* Secondary Forage Bush */
				avoid_actor_area 120 /* Tertiary Forage Bush */
				avoid_actor_area 130 /* Additional Forage Bush */
				avoid_actor_area 140 /* Custom Forage Bush */
				avoid_actor_area 150 /* Custom Forage Bush */
				avoid_actor_area 200 /* Primary Gold */
				avoid_actor_area 210 /* Secondary Gold */
				avoid_actor_area 220 /* Tertiary Gold */
				avoid_actor_area 230 /* Additional Gold */
				avoid_actor_area 240 /* Custom Gold */
				avoid_actor_area 250 /* Custom Gold */
				avoid_actor_area 300 /* Primary Stone */
				avoid_actor_area 310 /* Secondary Stone */
				avoid_actor_area 320 /* Tertiary Stone */
				avoid_actor_area 330 /* Additional Stone */
				avoid_actor_area 340 /* Custom Stone */
				avoid_actor_area 350 /* Custom Stone */

				avoid_actor_area 5100 /* King */
				avoid_actor_area 5110 /* Tower & Castle */
				avoid_actor_area 5350 /* Monastery */
				avoid_actor_area 5400 /* Wonder */

				avoid_actor_area 10100 /* Default Primary Forage Bush - Spacer */
				avoid_actor_area 10200 /* Default Primary Gold - Spacer */
				avoid_actor_area 10300 /* Default Primary Stone - Spacer */

				avoid_actor_area 11000 /* Neutral Forage Bush - Spacer */
				avoid_actor_area 11001 /* Neutral Forage Bush, Additional A - Spacer */
				avoid_actor_area 11002 /* Neutral Forage Bush, Additional B - Spacer */
				avoid_actor_area 11010 /* Neutral Gold - Spacer */
				avoid_actor_area 11011 /* Neutral Gold, Additional A - Spacer */
				avoid_actor_area 11012 /* Neutral Gold, Additional B - Spacer */
				avoid_actor_area 11020 /* Neutral Stone - Spacer */
				avoid_actor_area 11021 /* Neutral Stone, Additional - Spacer A */
				avoid_actor_area 11022 /* Neutral Stone, Additional - Spacer B */
			}

			create_object PLACEHOLDER_GENERIC /* Neutral Stone - Spacer */
			{
				number_of_objects 1024
				temp_min_distance_group_placement 1
				actor_area_to_place_in 1020

				actor_area 11020
				actor_area_radius 6
			}

			create_object HUNTABLE_SMALL_B
			{
				number_of_objects 3
				number_of_groups 1024
				set_loose_grouping
				group_placement_radius 2
				min_distance_to_players 52
				set_circular_placement
				temp_min_distance_group_placement 14

				min_distance_group_placement 1
				max_distance_to_other_zones 6
				avoid_forest_zone 2
				avoid_cliff_zone 2
				min_distance_to_map_edge 2

				actor_area 1040
				actor_area_radius 2

				avoid_actor_area 20 /* Villagers */
				avoid_actor_area 21 /* Villagers, Additional A */
				avoid_actor_area 22 /* Villagers, Additional B */
				avoid_actor_area 30 /* Scout */
				avoid_actor_area 31 /* Scout, Secondary */
				avoid_actor_area 32 /* Scout, Tertiary */
				avoid_actor_area 50 /* Walls */
				avoid_actor_area 51 /* Walls, Second Layer */
				avoid_actor_area 52 /* Walls, Third Layer */
				avoid_actor_area 60 /* Straggler, Spawn */
				avoid_actor_area 61 /* Straggler, Escalation Mode */

				avoid_actor_area 1000 /* Neutral Forage Bush */
				avoid_actor_area 1001 /* Neutral Forage Bush, Additional A */
				avoid_actor_area 1002 /* Neutral Forage Bush, Additional B */
				avoid_actor_area 1010 /* Neutral Gold */
				avoid_actor_area 1011 /* Neutral Gold, Additional A */
				avoid_actor_area 1012 /* Neutral Gold, Additional B */
				avoid_actor_area 1020 /* Neutral Stone */
				avoid_actor_area 1021 /* Neutral Stone, Additional A */
				avoid_actor_area 1022 /* Neutral Stone, Additional B */
				avoid_actor_area 1030 /* Neutral Small Herdable */
				avoid_actor_area 1031 /* Neutral Small Herdable, Additional A */
				avoid_actor_area 1032 /* Neutral Small Herdable, Additional B */
				avoid_actor_area 1035 /* Neutral Herdable */
				avoid_actor_area 1036 /* Neutral Herdable, Additional A */
				avoid_actor_area 1037 /* Neutral Herdable, Additional B */
				avoid_actor_area 1040 /* Neutral Small Huntable */
				avoid_actor_area 1041 /* Neutral Small Huntable, Additional A */
				avoid_actor_area 1042 /* Neutral Small Huntable, Additional B */
				avoid_actor_area 1045 /* Neutral Huntable */
				avoid_actor_area 1046 /* Neutral Huntable, Additional A */
				avoid_actor_area 1047 /* Neutral Huntable, Additional B */
				avoid_actor_area 1050 /* Neutral Lureable */
				avoid_actor_area 1051 /* Neutral Lureable, Additional A */
				avoid_actor_area 1052 /* Neutral Lureable, Additional B */
				avoid_actor_area 1060 /* Neutral Furbearers */
				avoid_actor_area 1061 /* Neutral Furbearers, Additional A */
				avoid_actor_area 1062 /* Neutral Furbearers, Additional B */

				avoid_actor_area 5100 /* King */
				avoid_actor_area 5110 /* Tower & Castle */
				avoid_actor_area 5350 /* Monastery */
				avoid_actor_area 5400 /* Wonder */

				avoid_actor_area 10102 /* Minimum Primary Forage Bush - Spacer */
				avoid_actor_area 10202 /* Minimum Primary Gold - Spacer */
				avoid_actor_area 10302 /* Minimum Primary Stone - Spacer */

				avoid_actor_area 11000 /* Neutral Forage Bush - Spacer */
				avoid_actor_area 11001 /* Neutral Forage Bush, Additional A - Spacer */
				avoid_actor_area 11002 /* Neutral Forage Bush, Additional B - Spacer */
			}

			create_object HUNTABLE_B
			{
				number_of_objects 3
				number_of_groups 1024
				set_loose_grouping
				group_placement_radius 2
				min_distance_to_players 52
				set_circular_placement
				temp_min_distance_group_placement 18

				min_distance_group_placement 1
				max_distance_to_other_zones 6
				avoid_forest_zone 2
				avoid_cliff_zone 2
				min_distance_to_map_edge 2

				actor_area 1045
				actor_area_radius 2

				avoid_actor_area 20 /* Villagers */
				avoid_actor_area 21 /* Villagers, Additional A */
				avoid_actor_area 22 /* Villagers, Additional B */
				avoid_actor_area 30 /* Scout */
				avoid_actor_area 31 /* Scout, Secondary */
				avoid_actor_area 32 /* Scout, Tertiary */
				avoid_actor_area 50 /* Walls */
				avoid_actor_area 51 /* Walls, Second Layer */
				avoid_actor_area 52 /* Walls, Third Layer */
				avoid_actor_area 60 /* Straggler, Spawn */
				avoid_actor_area 61 /* Straggler, Escalation Mode */

				avoid_actor_area 1000 /* Neutral Forage Bush */
				avoid_actor_area 1001 /* Neutral Forage Bush, Additional A */
				avoid_actor_area 1002 /* Neutral Forage Bush, Additional B */
				avoid_actor_area 1010 /* Neutral Gold */
				avoid_actor_area 1011 /* Neutral Gold, Additional A */
				avoid_actor_area 1012 /* Neutral Gold, Additional B */
				avoid_actor_area 1020 /* Neutral Stone */
				avoid_actor_area 1021 /* Neutral Stone, Additional A */
				avoid_actor_area 1022 /* Neutral Stone, Additional B */
				avoid_actor_area 1030 /* Neutral Small Herdable */
				avoid_actor_area 1031 /* Neutral Small Herdable, Additional A */
				avoid_actor_area 1032 /* Neutral Small Herdable, Additional B */
				avoid_actor_area 1035 /* Neutral Herdable */
				avoid_actor_area 1036 /* Neutral Herdable, Additional A */
				avoid_actor_area 1037 /* Neutral Herdable, Additional B */
				avoid_actor_area 1040 /* Neutral Small Huntable */
				avoid_actor_area 1041 /* Neutral Small Huntable, Additional A */
				avoid_actor_area 1042 /* Neutral Small Huntable, Additional B */
				avoid_actor_area 1045 /* Neutral Huntable */
				avoid_actor_area 1046 /* Neutral Huntable, Additional A */
				avoid_actor_area 1047 /* Neutral Huntable, Additional B */
				avoid_actor_area 1050 /* Neutral Lureable */
				avoid_actor_area 1051 /* Neutral Lureable, Additional A */
				avoid_actor_area 1052 /* Neutral Lureable, Additional B */
				avoid_actor_area 1060 /* Neutral Furbearers */
				avoid_actor_area 1061 /* Neutral Furbearers, Additional A */
				avoid_actor_area 1062 /* Neutral Furbearers, Additional B */

				avoid_actor_area 5100 /* King */
				avoid_actor_area 5110 /* Tower & Castle */
				avoid_actor_area 5350 /* Monastery */
				avoid_actor_area 5400 /* Wonder */

				avoid_actor_area 10102 /* Minimum Primary Forage Bush - Spacer */
				avoid_actor_area 10202 /* Minimum Primary Gold - Spacer */
				avoid_actor_area 10302 /* Minimum Primary Stone - Spacer */

				avoid_actor_area 11000 /* Neutral Forage Bush - Spacer */
				avoid_actor_area 11001 /* Neutral Forage Bush, Additional A - Spacer */
				avoid_actor_area 11002 /* Neutral Forage Bush, Additional B - Spacer */
			}
		endif

		#include_drs includes/neritic.inc

		#const REEDS_SPACING 12
		#include_drs includes/reeds.inc

		if DEATH_MATCH
		else
			#const PREDATOR_GROUPS 8
			#include_drs includes/predators.inc
			#include_drs includes/riparian.inc

			#const STRAGGLER_GROUPS 16
			#include_drs includes/stragglers_neutral.inc

			create_object FELLED_TREE
			{
				number_of_objects 1024
				min_distance_to_players 36
				set_circular_placement
				temp_min_distance_group_placement 16
				place_on_forest_zone

				actor_area 1300
				actor_area_radius 3
			}

			create_object FELLED_TREE
			{
				number_of_objects 1024
				min_distance_to_players 36
				set_circular_placement
				temp_min_distance_group_placement 16
				place_on_forest_zone

				actor_area 1300
				actor_area_radius 3
			}
		endif

		#define RELIC_TYPE_BALANCED

		#include_drs includes/relics.inc
		#include_drs includes/remote_resources.inc

		#define EXTRA_STUFF
		if EXTRA_STUFF
			if EMPIRE_WARS_MODE
				#define EMPIRE_WARS_FORAGERS

				#include_drs includes/empire_wars.inc
				#include_drs includes/empire_food.inc
				#include_drs includes/empire_gold.inc
				#include_drs includes/empire_wood.inc
				#include_drs includes/empire_buildings.inc
			elseif ESCALATION_MODE
				#include_drs includes/escalation_mode.inc
			endif

			#define AESTHETICS
			if AESTHETICS
				#include_drs includes/birds.inc

				create_object BIRD_A
				{
					number_of_objects rnd(2,3)
					set_scaling_to_map_size
				}

				create_object BIRD_B
				{
					number_of_objects rnd(1,2)
					set_scaling_to_map_size
				}

				create_object STUMP
				{
					number_of_objects 1024
					temp_min_distance_group_placement 8
					terrain_to_place_on SPAWN_TERRAIN
					actor_area_to_place_in 7000

					actor_area 0
					actor_area_radius 0

					avoid_actor_area 0 /* Self */
				}

				create_object STUMP
				{
					number_of_objects 1024
					temp_min_distance_group_placement 8
					terrain_to_place_on SPAWN_TERRAIN
					actor_area_to_place_in 7000

					actor_area 0
					actor_area_radius 0

					avoid_actor_area 0 /* Self */
				}

				create_object STUMP
				{
					number_of_objects 1024
					temp_min_distance_group_placement 8
					terrain_to_place_on SPAWN_TERRAIN
					actor_area_to_place_in 7000

					actor_area 0
					actor_area_radius 0

					avoid_actor_area 0 /* Self */
				}

				create_object OLD_STONE_HEAD
				{
					number_of_objects 1024
					temp_min_distance_group_placement 82
					min_distance_to_players 56
					set_circular_placement

					max_distance_to_other_zones 5
					avoid_forest_zone 2
					avoid_cliff_zone 2
					min_distance_to_map_edge 2

					actor_area 8000
					actor_area_radius 1

					avoid_actor_area 1000 /* Neutral Forage Bush */
					avoid_actor_area 1040 /* Neutral Small Huntable */
					avoid_actor_area 1045 /* Neutral Huntable */
				}

				create_object SKELETON
				{
					number_of_objects 1024
					temp_min_distance_group_placement 1
					actor_area_to_place_in 8000
				}
			endif
		endif
	endif
