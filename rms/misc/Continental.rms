
/* Continental for Forgotten Empires */

/* 2018 version with extra seasons */
/* updated for ANTIQUITY_MODE November 2023 */
/* Note: random_map.def is automatically included in every rms script */

/* PH SEASONS */

if ANTIQUITY_MODE
	start_random
		percent_chance 60 #define PH_MEDISOUTH
		percent_chance 40 #define PH_DESERT
	end_random
else
	start_random
		percent_chance 20 #define PH_SPRING
		percent_chance 10 #define PH_ALPINE
		percent_chance 5 #define PH_DESERT
		percent_chance 10 #define PH_TROPHICALSOUTH
		percent_chance 5 #define PH_TROPHICALEAST
		percent_chance 5 #define PH_AFRICAN_E
		percent_chance 15 #define PH_ASIAN
		percent_chance 5 #define PH_AUTUMN
		percent_chance 15 #define PH_FROZEN
		percent_chance 10 #define PH_MEDISOUTH
	end_random
endif
#include_drs F_seasons.inc

<PLAYER_SETUP>
  random_placement 
	ai_info_map_type CONTINENTAL 0 0 0

#include_drs thebr_setup.inc

<LAND_GENERATION>

base_terrain VODA

/* Fill in center */

create_land
{
  terrain_type                     LAYER_A
land_percent                       10
base_size 7
left_border                   25
right_border                  25
top_border                    25
bottom_border                 25
}

create_player_lands 
{            
  terrain_type                     LAYER_A
land_percent                       50
base_size                          12 /* originally 9 */

start_random
   percent_chance                  50
      left_border                   8
      right_border                  6
   percent_chance                  50
	left_border                   6
      right_border                  8
end_random
start_random
   percent_chance                  50
      top_border                    8
      bottom_border                 6
   percent_chance                  50
	top_border                    6
      bottom_border                 8
end_random

if BATTLE_ROYALE
	circle_radius 38
  border_fuzziness                   1
else
  border_fuzziness                   10
endif

  set_zone_randomly  
  other_zone_avoidance_distance    5
}

/* ****************************************************** */
<TERRAIN_GENERATION>

#include_drs F_ColorCorrection.inc

/* PRIMARY FOREST */

#define WMASK_VODA
#include_drs F_WaterMasking.inc

  create_terrain WOODIES
{
  base_terrain                   LAYER_A
  spacing_to_other_terrain_types 5
  land_percent                   7
  number_of_clumps               10
  set_avoid_player_start_areas     
  set_scale_by_groups
}
/* PRIMARY PATCH */

   create_terrain LAYER_B
{
  base_terrain                   LAYER_A
  number_of_clumps               16
  spacing_to_other_terrain_types 0
  land_percent                   20
  set_scale_by_size
}

   create_terrain DLC_ROCK
{
  base_terrain                   LAYER_B
  number_of_clumps               1000
  spacing_to_other_terrain_types 2
  land_percent                   100
  set_scale_by_size
}

   create_terrain LAYER_A
{
  base_terrain                   LAYER_B
  number_of_clumps               1000
  spacing_to_other_terrain_types 0
  land_percent                   100
  set_scale_by_size
terrain_mask 1
}

   create_terrain LAYER_B
{
  base_terrain                   DLC_ROCK
  number_of_clumps               1000
  spacing_to_other_terrain_types 0
  land_percent                   100
  set_scale_by_size
}

   create_terrain LAYER_B
{
  base_terrain                   DLC_ROCK
  number_of_clumps               1000
  spacing_to_other_terrain_types 0
  land_percent                   100
  set_scale_by_size
}

/* SECONDARY FOREST */

create_terrain WOODIES_B
{
base_terrain                   LAYER_B

  spacing_to_other_terrain_types 3
  land_percent                   1
  number_of_clumps               3
  set_avoid_player_start_areas     
  set_scale_by_groups
}

/* SECONDARY PATCH */

   create_terrain LAYER_C
{
  base_terrain                   LAYER_A
  number_of_clumps               24
  spacing_to_other_terrain_types 0
  land_percent                   2
  set_scale_by_size
  terrain_mask 1
}

/* LAYER E+F Patch */

   create_terrain LAYER_E
{
  base_terrain                   LAYER_A
  number_of_clumps               20
  spacing_to_other_terrain_types 0
  land_percent                   2
  set_scale_by_size
terrain_mask 1
}

   create_terrain LAYER_F
{
  base_terrain                   LAYER_A
  number_of_clumps               14
  spacing_to_other_terrain_types 0
  land_percent                   1
  set_scale_by_size
terrain_mask 1
}

<OBJECTS_GENERATION>

#define GNR_NORMALTC
#define GNR_STARTVILLS
#define GNR_CLASSICSCOUT

#define GNR_GIVERELICS

if TINY_MAP
#define GNR_RELICMODERN_PER2PL
#define GNR_RELIC_EXTRAONE
#define GNR_EXTRARELICAVOID40
else
#define GNR_RELICMODERN_MEDIUM
endif

#define GNR_REGICIDECLASSIC
#define GNR_6KER

#define GNR_STARTINGGOLD
#define GNR_STARTINGSTONE

#define GNR_STARTGOLD74M
#define GNR_STARTSTONE54CL
#define GNR_STARTGS_3WBLOCK
#define GNR_RESCLOSE

/* added to make sure Stone spawns correctly */
#define GNR_GIVEHERD_A
#define GNR_GIVEHERD_B

#define GNR_HERD4CLASSIC_A
#define GNR_HERD22CLASSIC_B

#define GNR_GIVEHUNT

#define GNR_HUNT3OR4CLASSIC

#define GNR_GIVELURES
#define GNR_CLASSICLURES

#define GNR_CLASSICPRED
#define GNR_RESBYMAP_KER

if TINY_MAP /* handled per player on 1v1 */
else
#define GNR_FARGORIG_GIVE
#define GNR_FARSORIG_GIVE

#define GNR_FARGORIG_TYPE1
#define GNR_FARSORIG_TYPE1
endif

#define GNR_HOMESTRAGGLE
#define GNR_NORMALBIRDS

#define GNR_STANDARDFISH

#define GNR_ADDITIONALPRED

#define GNR_MAPSTRAGGLE

#define GNR_GIVEFARHERD
#define GNR_FARHERD_INFINITE
#define GNR_FARHERD2GR
#define GNR_FARHERD80PLACE

#include_drs GeneratingObjects.inc

if INFINITE_RESOURCES
else
if ANTIQUITY_MODE 
create_object STONE
{
  number_of_objects 2
  number_of_groups 1
  group_placement_radius 2
  min_distance_to_players 28
  find_closest
  set_gaia_object_only
  set_place_for_every_player
  set_tight_grouping
  avoid_forest_zone 2
  avoid_cliff_zone 2
  max_distance_to_other_zones 3
}

create_object OYSTERS
{
  number_of_objects          2
  set_place_for_every_player
  number_of_groups 3
  min_distance_group_placement 10
  temp_min_distance_group_placement 20
  set_gaia_object_only
  find_closest_to_map_center
  if PH_FROZEN
  terrain_to_place_on ICY_SHORE
  else
  terrain_to_place_on BEACH
  endif
  ignore_terrain_restrictions
}
create_object OYSTERS
{
  number_of_objects          2
  set_place_for_every_player
  set_gaia_object_only
  set_tight_grouping
  find_closest
}
else
if TINY_MAP

create_object GOLD
{
  number_of_objects 3
  number_of_groups 1
  group_placement_radius 2
  min_distance_to_players 28
  find_closest
  set_gaia_object_only
  set_place_for_every_player
  set_tight_grouping
  avoid_forest_zone 2
  avoid_cliff_zone 2
  max_distance_to_other_zones 3
  actor_area 2951
  actor_area_radius 5
}


create_object STONE
{
  number_of_objects 2
  number_of_groups 1
  group_placement_radius 2
  min_distance_to_players 28
  find_closest
  set_gaia_object_only
  set_place_for_every_player
  set_tight_grouping
  avoid_forest_zone 2
  avoid_cliff_zone 2
  max_distance_to_other_zones 3
  avoid_actor_area 2951
}

endif
endif
/* ****************************************************** */

<ELEVATION_GENERATION>

#define GNRELEV_HEIGHT04
#define GNRELEV_INTENSITY_G
#define GNRELEV_LAY_A

#include_drs GeneratingElevation.inc

<CLIFF_GENERATION>

min_number_of_cliffs 5
max_number_of_cliffs 8
min_length_of_cliff  4
max_length_of_cliff  10
cliff_curliness      10
min_distance_cliffs  3     
cliff_type           CLIFF


/* ****************************************************** */

#const ICY_SHORE 37

<CONNECTION_GENERATION>
create_connect_all_players_land
{
if PH_FROZEN
	start_random
	percent_chance 50
  replace_terrain VODA	ICE
  replace_terrain MED_WATER     ICE
  replace_terrain DEEP_WATER    ICE
  replace_terrain DLC_WATER4   ICE
  replace_terrain ICY_SHORE	ICE
	percent_chance 50
  replace_terrain WATER         MELCINA
  replace_terrain MED_WATER     MELCINA
  replace_terrain DEEP_WATER    MELCINA
  replace_terrain DLC_WATER4   MELCINA
	end_random
else
  replace_terrain VODA	MELCINA
  replace_terrain MED_WATER     MELCINA
  replace_terrain DEEP_WATER    MELCINA
  replace_terrain DLC_WATER4   MELCINA
endif
  terrain_cost    VODA	7
  terrain_cost    MED_WATER     9
  terrain_cost    DEEP_WATER    15
  terrain_cost    DLC_WATER4	   15
  terrain_cost    WOODIES	7
  terrain_cost    WOODIES_B	7
  terrain_cost    MELCINA	3
  terrain_cost    LAYER_A	2
  terrain_cost    LAYER_B	2
  terrain_cost    LAYER_C	2
  terrain_cost    BEACH         7
  terrain_cost    ICY_SHORE	2
  terrain_size    VODA	3       1
  terrain_size    MED_WATER     3       1
  terrain_size    DEEP_WATER    3       1  
  terrain_size    DLC_WATER4	3       1  
  terrain_size    ICY_SHORE	3       1  
}