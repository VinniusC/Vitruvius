/* *****************************************************************************
 * Forest Michi - Made by Gatsuca 
 * *****************************************************************************
 * A Forest stands between each team in this wooded battlefield.
 * Chop and blow your way through the wall of trees to reach your foes!
 * ***************************************************************************** 
 * git: https://github.com/Gatsuca/AOE2_RMS
 * ***************************************************************************** */

/* Biome Selection from F_seasons.inc */
if ANTIQUITY_MODE
	start_random
		percent_chance 12 	#define PH_ALPINE
		percent_chance 12 	#define PH_ALPINE_B
		percent_chance 12 	#define PH_SPRING
		percent_chance 12 	#define PH_SPRING_B
		percent_chance 12 	#define PH_SPRING_C
		percent_chance 10 	#define PH_MEDISOUTH		
		percent_chance 12 	#define PH_AUTUMN
		percent_chance 10  	#define PH_AUTUMN_B
		percent_chance 8 	#define PH_FROZEN
	end_random
else 
	start_random
		percent_chance 8 	#define PH_ALPINE
		percent_chance 8 	#define PH_ALPINE_B
		percent_chance 7 	#define PH_SPRING
		percent_chance 7 	#define PH_SPRING_B
		percent_chance 7 	#define PH_SPRING_C
		percent_chance 12 	#define PH_MEDISOUTH
		percent_chance 5 	#define PH_TROPHICALSOUTH
		percent_chance 5 	#define PH_TROPHICALSOUTH_B
		percent_chance 8 	#define PH_TROPHICALEAST
		percent_chance 6 	#define PH_ASIAN
		percent_chance 6 	#define PH_ASIAN_B
		percent_chance 8 	#define PH_AUTUMN
		percent_chance 8 	#define PH_AUTUMN_B
		percent_chance 5 	#define PH_FROZEN
	end_random
endif
#define PH_EXTENDEDSEASONS

#include_drs F_seasons.inc

/* Declaration for storing map size scaling value by LENGTH */
if MAPSIZE_LUDICROUS 		
#const LENGTH 480		/* LUDICROUS 480x480 mapsize */
elseif MAPSIZE_MONSTROUS	
#const LENGTH 400		/* MONSTROUS 400x400 mapsize */
override_map_size 420 	/* Overrides mapsize generation */
elseif MAPSIZE_INCREDIBLE 	
#const LENGTH 360		/* INCREDIBLE 360x360 mapsize */
override_map_size 400 	/* Overrides mapsize generation */
elseif MAPSIZE_COLOSSAL 	
#const LENGTH 320		/* COLOSSAL 320x320 mapsize */
override_map_size 360 	/* Overrides mapsize generation */
elseif MAPSIZE_ENORMOUS 	
#const LENGTH 300		/* ENORMOUS 300x300 mapsize */
override_map_size 320 	/* Overrides mapsize generation */
elseif MAPSIZE_MASSIVE 		
#const LENGTH 276		/* MASSIVE 276x276 mapsize */
override_map_size 300 	/* Overrides mapsize generation */
elseif MAPSIZE_GIANT 		
#const LENGTH 252		/* GIANT 252x252 mapsize */
override_map_size 276 	/* Overrides mapsize generation */
elseif MAPSIZE_HUGE 		
#const LENGTH 240		/* HUGE 240x240 mapsize */
override_map_size 252 	/* Overrides mapsize generation */
elseif MAPSIZE_LARGE 		
#const LENGTH 220		/* LARGE 220x220 mapsize */
override_map_size 240 	/* Overrides mapsize generation */
elseif MAPSIZE_NORMAL 		
#const LENGTH 200		/* NORMAL 200x200 mapsize */
override_map_size 220 	/* Overrides mapsize generation */
elseif MAPSIZE_MEDIUM 		
#const LENGTH 168		/* MEDIUM 168x168 mapsize */
override_map_size 200 	/* Overrides mapsize generation */
elseif MAPSIZE_SMALL 		
#const LENGTH 144		/* SMALL 144x144 mapsize */
override_map_size 168 	/* Overrides mapsize generation */
elseif MAPSIZE_TINY 		
#const LENGTH 120		/* TINY 120x120 mapsize */
override_map_size 144 	/* Overrides mapsize generation */
elseif MAPSIZE_MINI 		
#const LENGTH 80		/* MINI 80x80 mapsize */
override_map_size 100 	/* Overrides mapsize generation IMPORTANT FOR 1v1 on MAPSIZE_MINI */
endif

/* PLAYER_SETUP: Configures how players are positioned and global parameters */
<PLAYER_SETUP>
if FIXED_POSITIONS
grouped_by_team  						/* Players positioned together in circle with variation */
else
random_placement  						/* Players positioned randomly in circle with variation */
endif
ai_info_map_type MICHI 0 1 1  			/* Tells AI: MapType=MICHI, IsNomad=0, IsMichi=1, ShowType=1 */
behavior_version 1						/* Used for versioning changes that might affect how existing maps generate */
/* Set water texture set for map based on biome */
if PH_ALPINE				water_definition WD_CHOPPY	
elseif PH_ALPINE_B			water_definition WD_CHOPPY	
elseif PH_SPRING			water_definition WD_CHOPPY	
elseif PH_SPRING_B			water_definition WD_STORMY	
elseif PH_SPRING_C			water_definition WD_CALM	
elseif PH_MEDISOUTH			water_definition WD_CALM	
elseif PH_TROPHICALSOUTH	water_definition WD_TROPICAL	
elseif PH_TROPHICALSOUTH_B	water_definition WD_TROPICAL	
elseif PH_TROPHICALEAST		water_definition WD_TROPICAL	
elseif PH_ASIAN 			water_definition WD_MURKY	
elseif PH_ASIAN_B			water_definition WD_MURKY	
elseif PH_AUTUMN			water_definition WD_CHOPPY	
elseif PH_AUTUMN_B			water_definition WD_STORMY	
elseif PH_FROZEN			water_definition WD_ARCTIC	
else
endif

/* LAND_GENERATION: Creates large terrain areas including player starting areas */
<LAND_GENERATION>
base_terrain WOODIES						/* Sets default terrain covering entire map */
/* If Gametype is King of the Hill, create a center island for Wonder */
if KING_OT_HILL								
create_land 
{
	terrain_type 				LAYER_A		/* Set the Terrain type for this land to LAYER_A */
	land_percent 			(LENGTH / 20)	/* land mass (LENGTH/20) */
	land_position 					50 50	/* Land located in center of map (50% 50%) */
	other_zone_avoidance_distance 	12		/* Minimum 12-tile gap between different zones */
	clumping_factor 				30		/* How much tiles prefer to be together next to other tiles of the same clump. */
	zone 							13		/* Land zone set to 13 */
	land_id 						13		/* Land id set to 13 */
}
else
endif
create_player_lands 						/* Creates a land for every player with specified parameters */
{
	generate_mode 1
	terrain_type	            LAYER_A  	/* Terrain type for player starting clearings */
	set_zone_by_team              			/* Team members share same zone - their lands can grow to touch each other */
	if LENGTH > 220							/* If mapsize is larger than LARGE, adjust map scaling */
	land_percent    	    		60  	/* 60% land mass */
	circle_radius 					45 1	/* Positions players in circle: 45% radius from center, 1 variance */
	base_size                     	24    	/* 24-tile square land origin size */
	other_zone_avoidance_distance 	20    	/* 20-tile distance between player zones */
	elseif LENGTH 220                       /* If mapsize is LARGE, adjust map scaling */
	land_percent    	    		50  	/* 50% land mass */
	circle_radius 					44 1	/* Positions players in circle: 44% radius from center, 1 variance */
	base_size 						22		/* 22-tile square land origin size */
	other_zone_avoidance_distance 	18    	/* 18-tile distance between player zones */
	elseif LENGTH 200                       /* If mapsize is NORMAL, adjust map scaling */
	land_percent    	    		45  	/* 45% land mass */
	circle_radius 					42 1	/* Positions players in circle: 42% radius from center, 1 variance */
	base_size                     	20    	/* 20-tile square land origin size */
	other_zone_avoidance_distance 	16    	/* 16-tile distance between player zones */
	elseif LENGTH 168                       /* If mapsize is MEDIUM, adjust map scaling */
	land_percent    	    		40  	/* 40% land mass */  
	circle_radius 					41 1	/* Positions players in circle: 41% radius from center, 1 variance */
	base_size                     	18    	/* 18-tile square land origin size */
	other_zone_avoidance_distance 	14    	/* 14-tile distance between player zones */
	elseif LENGTH 144                     	/* If mapsize is SMALL, adjust map scaling */
	land_percent    	    		35  	/* 35% land mass */  
	circle_radius 					40 1   	/* Positions players in circle: 40% radius from center, 1 variance */
	base_size                     	16    	/* 16-tile square land origin size */
	other_zone_avoidance_distance 	12    	/* 12-tile distance between player zones */
	elseif LENGTH 120                    	/* If mapsize is TINY, adjust map scaling */
	land_percent    	    		30	  	/* 30% land mass */  
	circle_radius 					39 1   	/* Positions players in circle: 39% radius from center, 1 variance */
	base_size                     	12    	/* 12-tile square land origin size */
	other_zone_avoidance_distance 	10    	/* 10-tile distance between player zones */
	elseif LENGTH < 120                     /* If mapsize is less than TINY, adjust map scaling */
	land_percent    	    		25 		/* 25% land mass */  
	circle_radius 					46 0   	/* Positions players in circle: 46% radius from center, 0 variance */
	base_size                     	8    	/* 8-tile square land origin size */
	other_zone_avoidance_distance 	8    	/* 8-tile distance between player zones */
	endif
	set_circular_base             			/* Makes land origin circular instead of square */
	clumping_factor 			rnd(4,8)	/* How much tiles prefer to be together next to other tiles of the same clump. */
}	

/* ELEVATION_GENERATION: Create hills */
<ELEVATION_GENERATION>
create_elevation 6 								/* Sets max height allowed in create_elevation (0-16) */
{
	base_terrain 				LAYER_A			/* Terrain type to apply elevation */
	set_scale_by_size							/* Scales tile count by map size */
	number_of_tiles 			2048			/* Amount of elevated tiles */
	number_of_clumps 			(LENGTH / 4)	/* Amount of hills LENGTH divided by 4 */
	enable_balanced_elevation					/* To keep elevation balanced across entirety of map */
}

/* TERRAIN_GENERATION: Replace terrains with other terrains */
<TERRAIN_GENERATION>
/* Color correction selection - Based upon Biome selected prior */
if PH_ALPINE				color_correction	CC_WINTER
elseif PH_ALPINE_B			color_correction	CC_WINTER
elseif PH_SPRING			color_correction	CC_SPRING
elseif PH_SPRING_B			color_correction	CC_SPRING
elseif PH_SPRING_C			color_correction	CC_SPRING
elseif PH_MEDISOUTH			color_correction	CC_SUMMER
elseif PH_TROPHICALSOUTH	color_correction	CC_RAINFOREST
elseif PH_TROPHICALSOUTH_B	color_correction	CC_RAINFOREST
elseif PH_TROPHICALEAST		color_correction	CC_JUNGLE
elseif PH_ASIAN 			color_correction	CC_SUMMER
elseif PH_ASIAN_B			color_correction	CC_STEPPES
elseif PH_AUTUMN			color_correction	CC_AUTUMN
elseif PH_AUTUMN_B			color_correction	CC_AUTUMN
elseif PH_FROZEN			color_correction	CC_ARCTIC
else
endif
/* Placing WOODIES */
create_terrain WOODIES
{
	base_terrain 					LAYER_A			/* Terrain type to create new terrain from */
	spacing_to_other_terrain_types 	0				/* Distance to other terrain types */
	land_percent 					(LENGTH / 20)	/* How much WOODIES is present on terrain in percent */
	number_of_clumps 				8				/* Number of clumps to be created */
	set_scale_by_groups								/* Scale clumps by map size */
	set_avoid_player_start_areas					/* Sets terrain generation to avoid player base area */
	clumping_factor 				rnd(8,10)		/* How much WOODIES prefer to be together next to other tiles of the same clump. */
}
/* Placing WOODIES_B */
create_terrain WOODIES_B
{
	base_terrain 					WOODIES			/* Terrain type to create new terrain from */
	spacing_to_other_terrain_types 	0				/* Distance to other terrain types */
	land_percent 					16				/* How much WOODIES_B is present on terrain in percent */
	number_of_clumps 				256				/* Number of clumps to be created */
	set_scale_by_groups								/* Scale clumps by map size */
	set_avoid_player_start_areas					/* Sets terrain generation to avoid player base area */
	clumping_factor 				rnd(5,10)		/* How much WOODIES_B prefer to be together next to other tiles of the same clump. */
}
/* Placing WOODIES_C */
create_terrain WOODIES_C
{
	base_terrain 					WOODIES			/* Terrain type to create new terrain from */
	spacing_to_other_terrain_types 	0				/* Distance to other terrain types */
	land_percent 					4				/* How much WOODIES_C is present on terrain in percent */
	number_of_clumps 				64				/* Number of clumps to be created */
	set_scale_by_groups								/* Scale clumps by map size */
	set_avoid_player_start_areas					/* Sets terrain generation to avoid player base area */
	clumping_factor 				rnd(2,4)		/* How much WOODIES_C prefer to be together next to other tiles of the same clump. */
}
/* Seasonal terrain mixing for land Decor */
create_terrain LAYER_B
{
	base_terrain 				LAYER_A				/* Terrain type to create new terrain from */
	land_percent 				12					/* How much LAYER_B is present on terrain in percent */
	number_of_clumps 			64					/* Number of clumps to be created */
	set_scale_by_groups								/* Scales clumps by map size */
	terrain_mask 				1					/* 1 - new terrain is masked over the base terrain and inherits its properties */
}
create_terrain LAYER_C
{
	base_terrain 				LAYER_A				/* Terrain type to create new terrain from */
	land_percent 				6					/* How much LAYER_C is present on terrain in percent */
	number_of_clumps 			32					/* Number of clumps to be created */
	set_scale_by_groups								/* Scales clumps by map size */
	terrain_mask 				1					/* 1 - new terrain is masked over the base terrain and inherits its properties */
}
create_terrain LAYER_E
{
	base_terrain 				LAYER_A				/* Terrain type to create new terrain from */
	land_percent 				3					/* How much LAYER_E is present on terrain in percent */
	number_of_clumps 			16					/* Number of clumps to be created */
	set_scale_by_groups								/* Scales clumps by map size */
	terrain_mask 				1					/* 1 - new terrain is masked over the base terrain and inherits its properties */
}

/* CONNECTION_GENERATION: Draws connections between lands and players */
<CONNECTION_GENERATION>
accumulate_connections 			/* Allows you to replace terrains created in previous connections with new terrains */
if FIXED_POSITIONS
/* Connect team TCs with setting specific paths */
create_connect_teams_lands 
{
	/* Replace all terrain in path with corresponding path */
	default_terrain_replacement CESTA									
	/* Terrain cost scale of 1-100 - lower values are more favorable - 0 = impassable  */	 
	terrain_cost 		LAYER_A 			10		
	terrain_cost 		LAYER_B 			10		
	terrain_cost 		LAYER_C 			10		
	terrain_cost 		LAYER_E 			10		
	terrain_cost 		WOODIES 			50		
	terrain_cost 		WOODIES_B 			50
	terrain_cost 		WOODIES_C 			50				
	/* Terrain size radius and variance for replaced terrain */ 
	terrain_size 		LAYER_A 			1 1	
	terrain_size 		WOODIES 			1 1		
	terrain_size  		WOODIES_B			1 1
	terrain_size  		WOODIES_C			1 1				
}		
else
endif

/* OBJECTS_GENERATION: Places units, buildings, and resources on the map */
<OBJECTS_GENERATION>
/* Town Center placement - placed at land origin for every player */
create_object TOWN_CENTER
{
	set_place_for_every_player    				/* Places TC for each player land */
	find_closest								/* Place at closest valid location to land origin */
}
/* Villager placement - automatic count based on civilization */
create_object VILLAGER
{
	set_place_for_every_player    				/* Places VILLAGER for each player land */
	min_distance_to_players 			3		/* Minimum distance of VILLAGER generation from player origin */
	max_distance_to_players 			5		/* Maximum distance of VILLAGER generation from player origin */
}	
/* Scout placement */	
create_object SCOUT	
{	
	set_place_for_every_player					/* Places SCOUT for each player land */
	min_distance_to_players 			4		/* Minimum distance of SCOUT generation from player origin */		
	max_distance_to_players 			6		/* Maximum distance of SCOUT generation from player origin */		
}
/* Herdables */
create_object HERDABLE_A
{
	number_of_objects 					2		/* How many HERDABLE_A per group */				
	number_of_groups 					2		/* How many groups of HERDABLE_A per player */
	set_gaia_object_only						/* Required for gaia objects placed per-player */
	set_place_for_every_player					/* Places HERDABLE_A for each player land */
	min_distance_to_players 			3		/* Minimum distance of HERDABLE_A generation from land origin */
	max_distance_to_players 			9		/* Maximum distance of HERDABLE_A generation from land origin */
	set_circular_placement						/* Uses circular distance instead of square */
	require_path 						1		/* Requires a path to the HERDABLE_A */
}	
create_object HERDABLE_B	
{	
	number_of_objects 					2		/* How many HERDABLE_B per group */				
	number_of_groups 					2		/* How many groups of HERDABLE_B per player */
	set_gaia_object_only						/* Required for gaia objects placed per-player */
	set_place_for_every_player					/* Places HERDABLE_B for each player land */
	min_distance_to_players 			3		/* Minimum distance of HERDABLE_B generation from land origin */
	max_distance_to_players 			9		/* Maximum distance of HERDABLE_B generation from land origin */
	set_circular_placement						/* Uses circular distance instead of square */
	require_path 						1		/* Requires a path to the HERDABLE_B */
}	
/* Huntables */	
create_object HUNTABLE 	
{	
	number_of_objects 					6		/* How many HUNTABLE per group */
	number_of_groups 					2		/* How many groups of HUNTABLE per player */
	group_placement_radius 				5		/* Number of tiles from the central tile that objects belonging to the same group may spawn */
	set_loose_grouping							/* Grouping type set to loose for this object */
	set_gaia_object_only						/* Required for gaia objects placed per-player */
	set_place_for_every_player					/* Places HUNTABLE for each player land */
	min_distance_to_players 			12		/* Minimum distance of HUNTABLE generation from player origin */
	max_distance_to_players 			24		/* Maximum distance of HUNTABLE generation from player origin */
	min_distance_group_placement 		8 		/* Minimum distance between group members */
	require_path 						1		/* Requires a path to the HUNTABLE */
}
/* Lurables */	
create_object LURABLE_A	
{	
	number_of_objects 					1		/* How many LURABLE_A per group */
	number_of_groups 					3		/* How many groups of LURABLE_A per player */
	set_gaia_object_only						/* Required for gaia objects placed per-player */
	set_place_for_every_player					/* Places LURABLE_A for each player land */
	min_distance_to_players 			12		/* Minimum distance of LURABLE_A generation from player origin */
	max_distance_to_players 			32		/* Maximum distance of LURABLE_A generation from player origin */
	min_distance_group_placement 		5 		/* Minimum distance between group members */
	require_path 						2		/* Requires a path to the LURABLE_A */
}	
create_object LURABLE_B	
{	
	number_of_objects 					1		/* How many LURABLE_B per group */
	number_of_groups 					2		/* How many groups of LURABLE_B per player */
	set_gaia_object_only						/* Required for gaia objects placed per-player */
	set_place_for_every_player					/* Places LURABLE_B for each player land */
	min_distance_to_players 			12		/* Minimum distance of LURABLE_B generation from player origin */
	max_distance_to_players 			32		/* Maximum distance of LURABLE_B generation from player origin */
	min_distance_group_placement 		5 		/* Minimum distance between group members */
	require_path 						2		/* Requires a path to the LURABLE_B */
}
/* Forage bushes  */	
create_object KERICEK 	
{	
	number_of_objects 					6		/* How many FORAGE tiles per group */
	number_of_groups 					1		/* How many groups of FORAGE per player */
	set_tight_grouping							/* Grouping type set to tight for this object */
	set_gaia_object_only						/* Required for gaia objects placed per-player */
	set_place_for_every_player					/* Places FORAGE for each player land */
	min_distance_to_players 			6		/* Minimum distance of FORAGE generation from land origin */
	max_distance_to_players 			10		/* Maximum distance of FORAGE generation from land origin */
	avoid_forest_zone 					2		/* Avoid FOREST tile to prevent trapped Resource */
	require_path 						1		/* Requires a path to the FORAGE */
}		
/* Relic Placement */	
create_object RELIC	
{	
	number_of_objects 					2		/* Number of RELIC per-player */
	set_scaling_to_player_number				/* Scales number of RELIC by player count */
	min_distance_to_players 			16		/* Minimum distance of RELIC generation from land origin */
	max_distance_to_players 			24		/* Maximum distance of RELIC generation from land origin */
	min_distance_to_map_edge			4		/* Minimum distance of RELIC generation from map edge */
	temp_min_distance_group_placement 	16		/* Avoid placement of this create_object type within 16 tiles */
	enable_tile_shuffling						/* Increases randomness of object positions */
}	
/* STRAGGLER trees */	
create_object STRAGGLER	
{	
	number_of_objects 					32		/* STRAGGLER_A per player */
	set_gaia_object_only        				/* Required for gaia objects placed per-player */
	set_place_for_every_player  				/* Places trees for each player land */
	terrain_to_place_on 			LAYER_A		/* Limits objects to LAYER_A */
	min_distance_to_players 			4   	/* 4 tiles from land origin */
	max_distance_to_players 			24		/* 24 tiles from land origin */
	set_circular_placement      				/* Uses circular distance instead of square */
	min_distance_group_placement 		0 		/* No minimum distance between group members */
	enable_tile_shuffling						/* Increases randomness of object positions */
}
/* Near Stone */
create_object STONE 
{
	number_of_objects 					4		/* How many STONE tiles per group */
	number_of_groups 					2		/* How many groups of STONE per player */
	set_tight_grouping							/* Grouping type set to tight for this STONE */
	set_gaia_object_only						/* Required for gaia objects placed per-player */
	set_place_for_every_player					/* Places STONE for each player land */
	min_distance_to_players 			8		/* Minimum distance of STONE generation from player origin */
	max_distance_to_players 			16		/* Maximum distance of STONE generation from player origin */
	avoid_forest_zone 					2		/* Avoid FOREST tile to prevent trapped Resource */
	enable_tile_shuffling						/* Increases randomness of object positions */
}	
/* Near Gold */	
create_object GOLD 	
{	
	number_of_objects 					4		/* How many GOLD tiles per group */
	number_of_groups 					2		/* How many groups of GOLD per player */
	set_tight_grouping							/* Grouping type set to tight for this GOLD */
	set_gaia_object_only						/* Required for gaia objects placed per-player */
	set_place_for_every_player					/* Places GOLD for each player land */
	min_distance_to_players 			10		/* Minimum distance of GOLD generation from player origin */
	max_distance_to_players 			16		/* Maximum distance of GOLD generation from player origin */
	min_distance_group_placement 		4		/* Minimum distance between group members */
	avoid_forest_zone 2							/* Avoid FOREST tile to prevent trapped Resource */
	enable_tile_shuffling						/* Increases randomness of object positions */
}
/* Far Gold Generation */
create_object GOLD 
{
	number_of_objects 					4	/* How many GOLD tiles per group */
	number_of_groups 					3	/* How many groups of GOLD per player */
	set_scaling_to_player_number			/* Scales groups to player count */
	set_tight_grouping						/* Grouping type set to tight for this GOLD */
	set_gaia_object_only					/* Required for gaia objects placed per-player */
	min_distance_to_players 			24	/* Minimum distance of GOLD generation from TC */
	max_distance_to_players 			48	/* Maximum distance of GOLD generation from TC */
	min_distance_group_placement 		4	/* Minimum distance between group members */
	temp_min_distance_group_placement 	40	/* Avoid placement of this create_object type within 40 tiles */
	avoid_forest_zone 					2	/* Avoid FOREST tile to prevent trapped Resource */
	enable_tile_shuffling					/* Increases randomness of object positions */
}
/* Far Stone Generation */
create_object STONE 
{
	number_of_objects 					4	/* How many STONE tiles per group */
	number_of_groups 					3	/* How many groups of STONE per player */
	set_scaling_to_player_number			/* Scales groups to player count */
	set_tight_grouping						/* Grouping type set to tight for this STONE */
	set_gaia_object_only					/* Required for gaia objects placed per-player */
	min_distance_to_players 			24	/* Minimum distance of STONE generation from TC */
	max_distance_to_players 			48	/* Maximum distance of STONE generation from TC */
	min_distance_group_placement 		4	/* Minimum distance between group members */
	temp_min_distance_group_placement 	40	/* Avoid placement of this create_object type within 40 tiles */
	avoid_forest_zone 					2	/* Avoid FOREST tile to prevent trapped Resource */
	enable_tile_shuffling					/* Increases randomness of object positions */
}
/* Predators */
create_object PREDATOR_A
{
	number_of_objects 					8			/* Number of PREDATOR_A per group */
	set_scaling_to_map_size							/* Scales object amount to map size */
	min_distance_to_players 			24			/* Minimum distance PREDATOR_A to be spawned in relation to players */
	temp_min_distance_group_placement 	16			/* Avoid placement of this create_object type within 16 tiles */
}
create_object PREDATOR_B
{
	number_of_objects 					8			/* Number of PREDATOR_B per group */
	set_scaling_to_map_size							/* Scales object amount to map size */
	min_distance_to_players 			24			/* Minimum distance PREDATOR_B to be spawned in relation to players */
	temp_min_distance_group_placement 	16			/* Avoid placement of this create_object type within 16 tiles */	
}
/* Birds */
create_object BIRDS_A
{
	number_of_objects 					4			/* Number of BIRDS_A per group */
	number_of_groups            		2		    /* Number of BIRDS_A groups */
	group_variance						2           /* BIRDS_A randomly varied by up to the amount specified */	
	set_scaling_to_map_size							/* Scales object amount to map size */
}
create_object BIRDS_B
{
	number_of_objects 					4			/* Number of BIRDS_B per group */
	number_of_groups            		2		    /* Number of BIRDS_B groups */
	group_variance						2           /* BIRDS_B randomly varied by up to the amount specified */	
	set_scaling_to_map_size							/* Scales object amount to map size */
}
/* Regicide game mode - spawns KING and CASTLE when Regicide is active */
if REGICIDE
create_object KING
{	
	set_place_for_every_player  				/* Places KING for each player land */
	min_distance_to_players 			4		/* Minimum distance of KING generation from player origin */
	find_closest                 				/* Placed at closest valid location to land origin */
}

create_object CASTLE
{
	set_place_for_every_player					/* Places CASTLE for each player land */
	min_distance_to_players 			8		/* Minimum distance of CASTLE generation from player origin */
	max_distance_to_players				16		/* Maximum distance of CASTLE generation from player origin */
	find_closest								/* Place at closest valid location to land origin */		
}
endif